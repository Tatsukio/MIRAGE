class CMirageAIMgr

	static var ^CMirageAIMgr ms_pxMrgAIInstance;
	var bool m_bAllowSupplySystemChecked;
	export var bool m_bAllowSupplySystem;
	var bool m_bResourcesUnlimitedChecked;
	export var bool m_bResourcesUnlimited;
	var bool m_bTransportHealingChecked;
	export var bool m_bTransportHealing;
	var bool m_bDiploLockedChecked;
	export var bool m_bDiploLocked;
	var bool m_bHeroPoolChecked;
	export var bool m_bHeroPool;
	var bool m_bFreeSpecialsChecked;
	export var bool m_bFreeSpecials;
	var bool m_bEpochSixChecked;
	export var bool m_bEpochSix;
	var bool m_bTitanSlotsChecked;
	export var bool m_bTitanSlots;
	var bool m_bFlyingEnabledChecked;
	export var bool m_bFlyingEnabled;
	var bool m_bRemoveTitansChecked;
	export var bool m_bRemoveTitans;
	var bool m_bBuildWallsChecked;
	export var bool m_bBuildWalls;
	var bool m_bLevelingAllowedChecked;
	export var bool m_bLevelingAllowed;
	export var bool m_bInited;
	var string m_sURSPath;
	
	var CAiObjWrap m_xPoolMixer;
	
	var CPropDB m_xLevelInfoDB;
	var ^CPropDB.CNode m_pxSupply;
	//ParaworldFan: ====DB to retrieve and apply custom map settings================================
	var ^CPropDB.CNode m_pxCustomMapSettingsDB;
	//==============================================================================================
	
	export proc bool Inited()
		var bool bResult=true;
		if(!AllowSupplySystem())then bResult=false; endif;
		if(!ResourcesUnlimited())then bResult=false; endif;
		if(!TransportHealing())then bResult=false; endif;
		if(!DiploLocked())then bResult=false; endif;
		if(!HeroPool())then bResult=false; endif;
		if(!FreeSpecials())then bResult=false; endif;
		if(!EpochSix())then bResult=false; endif;
		if(!TitanSlots())then bResult=false; endif;
		if(!FlyingEnabled())then bResult=false; endif;
		if(!RemoveTitans())then bResult=false; endif;
		if(!BuildWalls())then bResult=false; endif;
		return bResult;
	endproc;
	
	export static proc ref CMirageAIMgr Get()
		if(ms_pxMrgAIInstance==null)then
			ms_pxMrgAIInstance=new CMirageAIMgr();
		endif;
		if(ms_pxMrgAIInstance!=null&&ms_pxMrgAIInstance^.m_bInited==false)then
			var ^CAiBrain pxBrain;
			var int i=0;
			for(i=0)cond(i<8)iter(i++)do
				pxBrain=CAiWrap.GetBrain(i);
				if(pxBrain!=null)then break; endif;
			endfor;
			if(pxBrain==null)then return ms_pxMrgAIInstance^; endif;
			var ^CAiSensor pxSensor=^(pxBrain^.GetSensor());
			if(pxSensor==null)then return ms_pxMrgAIInstance^; endif;
			var string sGenericData=pxSensor^.GetGenericData().ToString();
			var string sURSRelPath=pxSensor^.GetUrsRelPath();
			if(sGenericData==""||sURSRelPath=="")then return ms_pxMrgAIInstance^; endif;
			var CPropDB xTestDB;
			if(!xTestDB.FromString(sGenericData))then return ms_pxMrgAIInstance^; endif;
			ms_pxMrgAIInstance^.Init(sGenericData,sURSRelPath);
			ms_pxMrgAIInstance^.m_bInited=true;
		endif;
		return ms_pxMrgAIInstance^;
	endproc;
	
	///ShutStatic
	export static proc void ShutStatic()
		delete ms_pxMrgAIInstance;
	endproc;
	
	export static proc void Kill()
		if(ms_pxMrgAIInstance!=null)then
			delete ms_pxMrgAIInstance;
			ms_pxMrgAIInstance=null;
		endif;
	endproc;
	
	export proc bool Purify()
		m_bAllowSupplySystemChecked=false;
		m_bResourcesUnlimitedChecked=false;
		m_bTransportHealingChecked=false;
		m_bDiploLockedChecked=false;
		m_bHeroPoolChecked=false;
		m_bFreeSpecialsChecked=false;
		m_bEpochSixChecked=false;
		m_bTitanSlotsChecked=false;
		m_bFlyingEnabledChecked=false;
		m_bRemoveTitansChecked=false;
		m_bBuildWallsChecked=false;
		m_bLevelingAllowedChecked=false;
		return true;
	endproc;
	
	constructor()
		m_xPoolMixer.FromInt(-1);
		m_bAllowSupplySystemChecked=false;
		m_bResourcesUnlimitedChecked=false;
		m_bTransportHealingChecked=false;
		m_bDiploLockedChecked=false;
		m_bHeroPoolChecked=false;
		m_bFreeSpecialsChecked=false;
		m_bEpochSixChecked=false;
		m_bTitanSlotsChecked=false;
		m_bFlyingEnabledChecked=false;
		m_bRemoveTitansChecked=false;
		m_bBuildWallsChecked=false;
		m_bLevelingAllowedChecked=false;
	endconstructor;
	
	destructor()
		m_xPoolMixer.FromInt(-1);
	enddestructor;
	
	export proc void Init(string p_sLIS,string p_sURSRelPath)
		m_sURSPath=p_sURSRelPath;
		m_xLevelInfoDB.FromString(p_sLIS);
		var ^CPropDB pxSupplies=new CPropDB;
		if(pxSupplies^.Load(p_sURSRelPath+"/Data/Base/Scripts/Server/settings/unit_supplies.txt"))then
			m_pxSupply=^((pxSupplies^).GetRoot());
		else
			m_pxSupply=null;
		endif;
		//ParaworldFan: Custom map level settings DB init
		var ^CPropDB pxCustomMapSettingsDB=new CPropDB;
		if(pxCustomMapSettingsDB^.Load(p_sURSRelPath+"/Data/Base/Scripts/Game/misc/CustomMapSettings.txt"))then
			m_pxCustomMapSettingsDB=^((pxCustomMapSettingsDB^).GetRoot());
		else
			m_pxCustomMapSettingsDB=null;
		endif;
		//m_xLevelInfoDB.Save("C:/LevelInfoDumpAI.txt");
		//KLog.LogInfo("CMirageAIMgr","Current AI settings are: " + m_xLevelInfoDB.ToString());
	endproc;
	
	export proc string GetURSPath()
		return m_sURSPath;
	endproc;
	
	export proc int GetSupply(string p_sCllsNm)
		if(m_pxSupply==null)then return 0; endif;
		var int iSuppCount=m_pxSupply^.GetValueI(p_sCllsNm, 0);
		return iSuppCount;
	endproc;
	
	proc ^CPropDB.CNode TryGetNode(string p_sNode, string p_sKey)
		if(p_sKey.IsEmpty())then
			KLog.LogError("CMirageAIMgr","Failed to get value from empty key");
			return null;
		endif;

		var ^CPropDB.CNode pxNode = ^((m_xLevelInfoDB)[p_sNode]);
		if(pxNode==null)then
			KLog.LogError("CMirageAIMgr","Failed to get value from key '" + p_sKey + "' - ClientSettings is null");
			return null;
		endif;

		var ^CPropDB.CNode pxValueNode = ^(pxNode^.Get(p_sKey));
		if(pxValueNode == null)then
			KLog.LogError("CMirageAIMgr","Failed to get value from key '" + p_sKey + "' - ValueNode is null");
			return null;
		endif;

		return pxValueNode;
	endproc;

	proc bool TryGetValue(string p_sSettingsPath, string p_sKey, string p_sPoolKey, ref bool p_rbReturnValue, bool p_bFallbackValue)
		p_rbReturnValue = p_bFallbackValue;

		var ^CPropDB.CNode pxValueNode = TryGetNode("ClientSettings", p_sKey);
		if(pxValueNode == null)then
			if(!m_xPoolMixer.IsValid())then InitMixer(); endif;
			if(m_xPoolMixer.IsValid())then
				p_rbReturnValue = m_xPoolMixer.GetAttribValueInt(p_sPoolKey) == 1;
				return m_xPoolMixer.GetAttribValueInt("inited") == 1;
			endif;
			KLog.LogError("CMirageAIMgr","Failed to get value from key '" + p_sKey + "' - invalid PoolMixer");
			return false;
		endif;

		p_rbReturnValue = pxValueNode^.ValueI() == 1;
		KLog.LogInfo("CMirageAIMgr", "Setting '" + p_sKey + "' = '" + p_rbReturnValue.ToString() + "'");
		return true;
	endproc;
	
	proc bool TryGetValue(string p_sSettingsPath, string p_sKey, string p_sPoolKey, ref int p_riReturnValue, int p_iFallbackValue)
		p_riReturnValue = p_iFallbackValue;
		var ^CPropDB.CNode pxValueNode = TryGetNode("ClientSettings", p_sKey);
		if(pxValueNode == null)then
			if(p_sPoolKey == "")then
				KLog.LogError("CMirageAIMgr","Failed to get value from key '" + p_sKey + "'");
				return false;
			endif;
			if(!m_xPoolMixer.IsValid())then InitMixer(); endif;
			if(m_xPoolMixer.IsValid())then
				p_riReturnValue = m_xPoolMixer.GetAttribValueInt(p_sPoolKey);
				return m_xPoolMixer.GetAttribValueInt("inited") == 1;
			endif;
			KLog.LogError("CMirageAIMgr","Failed to get value from key '" + p_sKey + "' - invalid PoolMixer");
			return false;
		endif;
		p_riReturnValue = pxValueNode^.ValueI();
		KLog.LogInfo("CMirageAIMgr", "Setting '" + p_sKey + "' = '" + p_riReturnValue.ToString() + "'");
		return true;
	endproc;
	
	export proc bool AllowSupplySystem()
		if(m_bAllowSupplySystemChecked)then
			return m_bAllowSupplySystem;
		endif;

		m_bAllowSupplySystemChecked = TryGetValue("Server/GameplayOptions/", "UseSupply", "supply_system", m_bAllowSupplySystem, false);
		return m_bAllowSupplySystem;
	endproc;
	
	export proc bool DiploLocked()
		if(m_bDiploLockedChecked)then
			return m_bDiploLocked;
		endif;

		m_bDiploLockedChecked = TryGetValue("Server/GameplayOptions/", "DiploLocked", "", m_bDiploLocked, false);
		return m_bDiploLocked;
	endproc;
	
	export proc bool ResourcesUnlimited()
		if(m_bResourcesUnlimitedChecked)then
			return m_bResourcesUnlimited;
		endif;

		m_bResourcesUnlimitedChecked = TryGetValue("Server/GameplayOptions/", "ResourcesUnlimited", "unlimited_storage", m_bResourcesUnlimited, false);
		return m_bResourcesUnlimited;
	endproc;
	
	export proc bool TransportHealing()
		if(m_bTransportHealingChecked)then
			return m_bTransportHealing;
		endif;

		m_bTransportHealingChecked = TryGetValue("Server/GameplayOptions/", "TransportHealing", "tbasket_healing", m_bTransportHealing, false);
		return m_bTransportHealing;
	endproc;
	
	export proc bool HeroPool()
		if(m_bHeroPoolChecked)then
			return m_bHeroPool;
		endif;

		m_bHeroPoolChecked = TryGetValue("Server/GameplayOptions/", "HeroPool", "hero_pool", m_bHeroPool, false);
		return m_bHeroPool;
	endproc;
	
	export proc bool FreeSpecials()
		if(m_bFreeSpecialsChecked)then
			return m_bFreeSpecials;
		endif;

		m_bFreeSpecialsChecked = TryGetValue("Server/GameplayOptions/", "FreeSpecials", "free_specials", m_bFreeSpecials, false);
		return m_bFreeSpecials;
	endproc;
	
	export proc bool EpochSix()
		if(m_bEpochSixChecked)then
			return m_bEpochSix;
		endif;

		m_bEpochSixChecked = TryGetValue("Server/GameplayOptions/", "EpochSix", "epoch_six", m_bEpochSix, false);
		return m_bEpochSix;
	endproc;
	
	export proc bool TitanSlots()
		if(m_bTitanSlotsChecked)then
			return m_bTitanSlots;
		endif;

		m_bTitanSlotsChecked = TryGetValue("Server/GameplayOptions/", "TitanSlots", "titan_slots", m_bTitanSlots, false);
		return m_bTitanSlots;
	endproc;
	
	export proc bool FlyingEnabled()
		if(m_bFlyingEnabledChecked)then
			return m_bFlyingEnabled;
		endif;

		m_bFlyingEnabledChecked = TryGetValue("Server/GameplayOptions/", "FlyingEnabled", "flying_enabled", m_bFlyingEnabled, false);
		return m_bFlyingEnabled;
	endproc;
	
	export proc bool RemoveTitans()
		if(m_bRemoveTitansChecked)then
			return m_bRemoveTitans;
		endif;

		m_bRemoveTitansChecked = TryGetValue("Server/GameplayOptions/", "RemoveTitans", "titans_disabled", m_bRemoveTitans, false);
		return m_bRemoveTitans;
	endproc;
	
	export proc bool BuildWalls()
		if(!m_bBuildWallsChecked)then
			m_bBuildWalls = false;
			var int iBuildWalls = 0;
			var ^CPropDB.CNode pxValueNode = TryGetNode("AIOptions", "walls");
			if(pxValueNode == null)then
				KLog.LogError("CMirageAIMgr","Failed to get value from key 'walls' - ValueNode is null");
				return false;
			endif;
			iBuildWalls = pxValueNode^.GetValueI("walls");
			m_bBuildWalls = iBuildWalls == 1;
			m_bBuildWallsChecked = true;
			KLog.LogInfo("CMirageAIMgr", "Setting 'AIOptions/walls' = '" + m_bBuildWalls.ToString() + "'");
			return m_bBuildWalls;
		else
			return m_bBuildWalls;
		endif;
	endproc;
	
	export proc bool LevelingAllowed()
		if(m_bLevelingAllowedChecked)then
			return m_bLevelingAllowed;
		endif;

		var int iDwnLvlSwitch = 0;
		m_bLevelingAllowedChecked = TryGetValue("Server/GameplayOptions/", "DwnLvlSwitch", "leveling_allowed", iDwnLvlSwitch, 0);
		if(iDwnLvlSwitch > 0)then
			m_bLevelingAllowed = true;
		endif;
		return m_bLevelingAllowed;
	endproc;
	
	proc bool InitMixer()
		var CAiObjQuery xMixerQuery;
		var CAiObjWrapList xMixers;
		xMixers.Clear();
		xMixerQuery.SetOwner(-1);
		xMixerQuery.SetClass("pool_mixer");
		xMixerQuery.Execute(xMixers);
		xMixers.Validate();
		var int i, iC=xMixers.NumEntries();
		if(iC<=0)then return false; endif;
		m_xPoolMixer = xMixers[0];
		return true;
	endproc;
	
	export proc bool CheckCustomMapAISetting(string p_sLevelName, string p_sPlayerID, string p_sSetting)
//		KLog.LogSpam("ParaworldFan-ALERT", "MirageAIMgr:CheckCustomMapAISetting("+p_sLevelName+", "+p_sSetting+") begin");

		if(p_sLevelName.IsEmpty() || p_sSetting.IsEmpty())then
			KLog.LogError("CMirageAIMgr", "Failed to check custom map AI setting - levelname or key is empty");
			return false;
		endif;
		
		if(m_pxCustomMapSettingsDB == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - m_pxCustomMapSettingsDB is null");
			return false;
		endif;
		
		var ^CPropDB.CNode pxLevelsNode=m_pxCustomMapSettingsDB^.Get("Levels");
		if(pxLevelsNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxLevelsNode is null");
			return false;
		endif;
		
		p_sLevelName.Replace(" ", "_");
		var ^CPropDB.CNode pxLevelNameNode = pxLevelsNode^.Get(p_sLevelName);
		if(pxLevelNameNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxLevelNameNode is null");
			return false;
		endif;
		
		var ^CPropDB.CNode pxSettingsNode = pxLevelNameNode^.Get("Custom");
		if(pxSettingsNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxSettingsNode is null");
			return false;
		endif;

		var ^CPropDB.CNode pxAiNode = pxSettingsNode^.Get("Players");
		if(pxAiNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxAiNode is null");
			return false;
		endif;
		
		var ^CPropDB.CNode pxAiPlayerNode = pxAiNode^.Get("Player_" + p_sPlayerID);
		if(pxAiPlayerNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxAiPlayerNode is null");
			return false;
		endif;

		var ^CPropDB.CNode pxAiSettingNode = pxAiPlayerNode^.FindNode(p_sSetting, false);
		if(pxAiSettingNode == null)then
			KLog.LogWarn("CMirageAIMgr", "Failed to check custom map AI setting - pxAiSettingNode is null");
			return false;
		endif;

		var string sValue = pxAiSettingNode^.Value();
		KLog.LogInfo("CMirageAIMgr", "Setting '" + p_sSetting + "' = '" + sValue + "'");
		return (sValue=="" || sValue.ToInt()==1);
	endproc;
	
endclass;
