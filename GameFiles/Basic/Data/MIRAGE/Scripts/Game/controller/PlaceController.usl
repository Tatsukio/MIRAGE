class CPlaceController inherit CInputController

	var color 			m_xPlaceable;
	var color 			m_xNotPlaceable;

	var bool			m_bActive;
	var string			m_sGfxClass;
	var string			m_sClass;
	var string			m_sObjClass;
	var string			m_sTTPath;
	var string			m_sAction;
	var string			m_sTribe;
	var bool			m_bWall;
	var bool			m_bGate;
	var bool			m_bTower;
	var bool			m_bTrap;
	var bool			m_bSiege;
	var bool			m_bInvalid;
	var bool			m_bRallyPoint;
	var bool			m_bCoastal;

	var bool 			m_bLBtnHovered;
	var bool			m_bRotateBegin;
	var bool			m_bPlaceBegin;
	var real			m_fGhostRot;
	var int				m_iBuildingRotSteps;

	var int				m_iNumGhostsInTemplate;
	var int				m_iNumTemplates;
	var vec2 			m_vTemplateSaveZone;
	var array vec2		m_avTemplatesXYMatrix;
	var array vec3		m_avPos;

	var bool			m_bValid;

	var vec3			m_vPlacement;
	var point			m_xMousePlacement;
	var int				m_iResNumPossible;

	var int				m_iNumTilesHold;

	var CWallMap.CWall	m_xWall;

	export constructor()
		m_xPlaceable = {{0,255,0,255}};
		m_xNotPlaceable = {{255,0,0,255}};
		
		m_bActive=false;
		m_bValid=false;

		m_bWall=false;
		m_bGate=false;
		m_bTower=false;
		m_bTrap=false;
		m_bSiege=false;
		m_bInvalid=false;
		m_bCoastal=false;
		m_bRallyPoint=false;
		m_vPlacement={0.0,0.0,0.0};
		m_fGhostRot=0.0;

		m_iNumGhostsInTemplate = 1;
		m_iNumTemplates = 0;
		m_avPos = 8;
		m_avTemplatesXYMatrix = 8;
		m_avTemplatesXYMatrix[0] = {-1.0,0.0};
		m_avTemplatesXYMatrix[1] = {1.0,0.0};
		m_avTemplatesXYMatrix[2] = {0.0,1.0};
		m_avTemplatesXYMatrix[3] = {-1.0,1.0};
		m_avTemplatesXYMatrix[4] = {1.0,1.0};
		m_avTemplatesXYMatrix[5] = {0.0,-1.0};
		m_avTemplatesXYMatrix[6] = {-1.0,-1.0};
		m_avTemplatesXYMatrix[7] = {1.0,-1.0};
		/*
		m_avTemplatesXYMatrix[8] = {-2.0,0.0};
		m_avTemplatesXYMatrix[9] = {2.0,0.0};
		m_avTemplatesXYMatrix[10] = {-2.0,1.0};
		m_avTemplatesXYMatrix[11] = {2.0,1.0};
		m_avTemplatesXYMatrix[12] = {-2.0,-1.0};
		m_avTemplatesXYMatrix[13] = {2.0,-1.0};
		m_avTemplatesXYMatrix[14] = {0.0,2.0};
		m_avTemplatesXYMatrix[15] = {-1.0,2.0};
		m_avTemplatesXYMatrix[16] = {1.0,2.0};
		m_avTemplatesXYMatrix[17] = {-2.0,2.0};
		m_avTemplatesXYMatrix[18] = {2.0,2.0};
		m_avTemplatesXYMatrix[19] = {0.0,-2.0};
		m_avTemplatesXYMatrix[20] = {-1.0,-2.0};
		m_avTemplatesXYMatrix[21] = {1.0,-2.0};
		m_avTemplatesXYMatrix[22] = {-2.0,-2.0};
		m_avTemplatesXYMatrix[23] = {2.0,-2.0};
		*/
		
		m_bLBtnHovered = false;
		m_bRotateBegin = false;
		m_bPlaceBegin = false;
		
		CClientWrap.GetObjMgr().GetObjPlaceChecker()^.SetGridSize(15);
		
		if(!CSettings.Get("Game/GUIOptions/BuildingRotation/Steps",m_iBuildingRotSteps))then
			m_iBuildingRotSteps=16;
			CSettings.Set("Game/GUIOptions/BuildingRotation/Steps",m_iBuildingRotSteps);
		else
			m_iBuildingRotSteps=Math.Clamp(m_iBuildingRotSteps,1,360); // 64 before
		endif;
		m_iNumTilesHold=0;
	endconstructor;
	
	export proc void Reset()
		m_bWall=false;
		m_bGate=false;
		m_bTower=false;
		m_bTrap=false;
		m_bSiege=false;
		m_bInvalid=false;
		m_bCoastal=false;
		m_bRallyPoint=false;
		m_vPlacement={0.0,0.0,0.0};
		m_fGhostRot=0.0;
		m_bLBtnHovered = false;
		m_bRotateBegin = false;
		m_bPlaceBegin = false;
		m_iNumGhostsInTemplate = 1;
		m_iNumTemplates = 0;
		ClearTemplatesPos();
		if(!CSettings.Get("Game/GUIOptions/BuildingRotation/Steps",m_iBuildingRotSteps))then
			m_iBuildingRotSteps=16;
			CSettings.Set("Game/GUIOptions/BuildingRotation/Steps",m_iBuildingRotSteps);
		else
			m_iBuildingRotSteps=Math.Clamp(m_iBuildingRotSteps,1,360); // 64 before
		endif;
		m_bActive=false;
		CClientWrap.GetObjMgr().DeleteGhost();
		ClearWall();
		CClientWrap.GetObjMgr().GetObjPlaceChecker()^.EnableGridRendering(false);
		CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap()^.EnableGridRendering(false);
	endproc;
	
	export proc void ClearWall()
		m_iNumTilesHold=0;
		m_xWall.Clear();
	endproc;
	
	export proc void ClearTemplatesPos()
		var int i, iC = m_avPos.NumEntries();
		for(i=0)cond(i<iC)iter(++i)do
			m_avPos.DeleteEntry(i);
			i--;
			iC--;
		endfor;
	endproc;
	
	export proc void Render()
		if(!m_bActive || m_bValid)then return; endif;
		if(!m_bWall && !m_bTower && !m_bTrap)then
			//if(!m_bRotateBegin || m_bLBtnHovered)then
			//	CheckPlacebility();
			//endif;
			CheckPlacebility();
		else
			AddWall();
		endif;
		m_bValid=true;
	endproc;

	export proc void SetResNumPossible(int p_iResNumPossible)
		m_iResNumPossible=p_iResNumPossible;
	endproc;
	
	export proc string GetTTPath()
		return m_sTTPath;
	endproc;
	
	proc int GetBuildingCount(string p_sClass)
		var int iCount=-1;
		var ^CPlayer pxPlayer = ^(CGameWrap.GetClient().GetPlayer());
		if(pxPlayer==null)then return iCount; endif;
		var CObjList xBldg=pxPlayer^.GetBuildings();
		var int i,iC=xBldg.NumEntries();
		iCount=iC;
		for(i=0)cond(i<iC)iter(i++)do
			if(!CompareClass(xBldg[i],p_sClass))then
				iCount-=1;
			endif;
		endfor;
		return iCount;
	endproc;
	
	proc bool CompareClass(CClntHndl p_xHandle, string p_sClass)
		if(!p_xHandle.IsValid())then return false; endif;
		var ^CObj pxObj = p_xHandle.GetObj();
		if(pxObj==null)then return false; endif;
		var string sClass=pxObj^.GetClassName();
		if(sClass=="Virtual_Produce_Unit")then return false; endif;
		var ^CAttribs pxAttr=pxObj^.GetAttribs();
		if(p_sClass.Find("aje_")!=-1&&pxAttr!=null)then
			var string sFlag = pxAttr^.GetValue("ObjFlag");
			if(sFlag!="")then
				return p_sClass==sFlag;
			endif;
		endif;
		return p_sClass==sClass;
	endproc;
	
	export proc void SetClass(string p_sObjClass, string p_sAction)
		var ^CPlayer pxPlayer = ^(CGameWrap.GetClient().GetPlayer());
		if(pxPlayer==null)then
			CGameWrap.LogError("Controller.Place","Player invalid!");
			return;
		endif;

		var CTechTree xTechTree = pxPlayer^.GetPlayerTechTree();
		var string sObjGfx = xTechTree.GetValueS(p_sObjClass+"/gfx","Dummy");
		if(sObjGfx=="Dummy")then
			CGameWrap.LogError("Controller.Place","Invalid Gfx: "+p_sObjClass);
			return;
		endif;
		
		CClientWrap.GetObjMgr().DeleteGhost();
		m_fGhostRot=0.0;
		m_vPlacement = GetPickpoint();

		m_bActive = true;
		m_bValid = false;
		m_sGfxClass = sObjGfx;
		m_sObjClass = p_sObjClass;
		m_sClass = p_sObjClass.Mid(p_sObjClass.FindRev('/') + 1);

		//Legacy with server support (values from tt)
		m_bWall = (xTechTree.GetValueI(p_sObjClass+"/wall",0) != 0);
		m_bGate = (xTechTree.GetValueI(p_sObjClass+"/gate",0) != 0);
		m_bTower = (xTechTree.GetValueI(p_sObjClass+"/tower",0) != 0);
		m_bTrap = (xTechTree.GetValueI(p_sObjClass+"/trap",0) != 0);
		m_bCoastal = (xTechTree.GetValueI(p_sObjClass+"/coastal",0) != 0);

		/*
		//New without server support (values from wallclassconfig_ex.txt)
		var bitset dwWC = CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap()^.GetWallClass(m_sClass);
		m_bWall = (dwWC == CWallMapCln.WC_Wall);
		m_bGate = (dwWC == CWallMapCln.WC_Gate);
		m_bTower = (dwWC == CWallMapCln.WC_Tower);
		m_bTrap = (dwWC == CWallMapCln.WC_Trap);
		m_bSiege = (dwWC == CWallMapCln.WC_Siege);
		m_bInvalid = (dwWC == CWallMapCln.WC_Invalid);
		*/

		if(p_sObjClass.Find("rally_point")>=0)then
			m_bCoastal = false;
			m_bRallyPoint = true;
		else
			m_bRallyPoint=false;
		endif;

		var string sChunk = p_sObjClass;
		sChunk.TrimLeft('/');
		sChunk = sChunk.Mid(sChunk.Find('/') + 1);
		m_sTribe = sChunk.Left(sChunk.Find('/'));

		if(m_sTribe != "Aje" && m_sTribe != "Hu" && m_sTribe != "Ninigi" && m_sTribe != "SEAS" && m_sTribe != "Special")then
			m_sTribe = pxPlayer^.GetPlayerSlot()^.GetTribe();
		endif;
		m_sTTPath = p_sObjClass;
		m_sAction = p_sAction;
		if(m_bWall || m_bTower || m_bTrap)then
			m_bPlaceBegin=false;
			CClientWrap.GetObjMgr().GetObjPlaceChecker()^.EnableGridRendering(false);
			CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap()^.EnableGridRendering(true);
		else
			var string sBuilding=p_sAction.Right(p_sAction.GetLength()-(p_sAction.FindRev('/')+1));
			if(m_sGfxClass!=sBuilding && m_sGfxClass.Find("rally_point")<0 && xTechTree.GetValueS(p_sObjClass+"/ghost","").IsEmpty())then
				m_sGfxClass=sBuilding;
			endif;

			if(m_sGfxClass.Find("_launchpad_zeppelin")!=-1)then
				AddGhostAdvanced(m_sGfxClass, 2);
			elseif(m_sGfxClass=="seas_wall_gate")then
				AddGhostAdvanced(m_sGfxClass, {0.0,0.0,-1.0});
			elseif(m_sGfxClass=="seas_fortress")then
				AddGhostAdvanced(m_sGfxClass, {0.0,0.0,3.99});
			elseif(m_sGfxClass=="aje_sensor_tower")then
				AddGhostAdvanced(m_sGfxClass, {0.0,0.0,-11.0});
			elseif(m_sGfxClass=="hu_launchpad_colossi")then
				AddGhostAdvanced("hu_colossi");
			elseif(m_sGfxClass=="hu_mud_pit")then
				AddGhostAdvanced(m_sGfxClass, {0.0,0.0,0.5});
			elseif(m_sGfxClass=="seas_shore_cannon")then
				AddGhostAdvanced(m_sGfxClass, {0.0,0.0,-7.05});
			else
				AddGhostAdvanced(m_sGfxClass);
			endif;
			
			if(m_sGfxClass=="aje_floating_harbour")then
				AddGhostAdvanced("Macrolemys_Water", "Cr_1");
				AddGhostAdvanced("Macrolemys_Water", "Cr_2");
				AddGhostAdvanced("Macrolemys_Water", "Cr_3");
				AddGhostAdvanced("Macrolemys_Water", "Cr_4");
			elseif(m_sGfxClass=="hu_fireplace" && pxPlayer^.GetAttribs()^.GetValueInt("final") == 6)then
				AddGhostAdvanced("hu_final_stage_deco_0", {-0.5,11.0,0.0});
				AddGhostAdvanced("hu_final_stage_deco_1", {-5.38,2.74,0.0});
			elseif(m_sGfxClass=="hu_harbour")then
				AddGhostAdvanced("hu_harbour_crane", "Cr_3");
			elseif(m_sGfxClass=="ninigi_fireplace" && pxPlayer^.GetAttribs()^.GetValueInt("final") == 6)then
				AddGhostAdvanced("ninigi_final_stage_deco_0", {-7.35,-7.86,-2.9}, {0.0,0.0,3.141593});
				AddGhostAdvanced("ninigi_final_stage_deco_1", {6.36,-7.94,-2.9}, {0.0,0.0,1.570796});
			elseif(m_sGfxClass=="ninigi_launchpad_pirate_boss")then
				AddGhostAdvanced("pirate_boss_tail", {0.0,-11.36,0.0});
				AddGhostAdvanced("pirate_boss_sail");
				AddGhostAdvanced("pirate_boss_row", {0.0,12.6,0.0});
				AddGhostAdvanced("pirate_boss_cannons");
			elseif(m_sGfxClass=="ninigi_rocket_ramp")then
				AddGhostAdvanced("ninigi_rb_top", "RE_1");
				AddGhostAdvanced("ninigi_rb_bird", 1, "we");
			elseif(m_sGfxClass=="seas_headquarters" && pxPlayer^.GetAttribs()^.GetValueInt("final") == 6)then
				AddGhostAdvanced("seas_final_stage_deco_0", {-3.31,-4.11,-14.06});
				AddGhostAdvanced("seas_final_stage_deco_1", {-0.05,7.99,-0.52});
				AddGhostAdvanced("seas_final_stage_deco_2", {0.08,3.98,-0.52}, {0.0,0.0,3.141593});
				AddGhostAdvanced("seas_final_stage_deco_3", {-8.77,8.71,-3.98});
			elseif(m_sGfxClass=="seas_carrier")then
				AddGhostAdvanced("seas_carrier_turret", "we");
			elseif(m_sGfxClass=="seas_shore_cannon")then
				AddGhostAdvanced("seas_shore_cannon", "we");
			elseif(m_sGfxClass=="seas_mg_nest")then
				AddGhostAdvanced("seas_hq_machinegun_nest_top", "we");
			elseif(m_sGfxClass=="seas_mine")then
				AddGhostAdvanced("seas_mine_misc_0", {1.82,6.54,0.0});
				AddGhostAdvanced("seas_mine_misc_1", {-1.75,4.54,3.01});
				AddGhostAdvanced("seas_mine_misc_2", {-4.89,-0.41,-3.07});
				AddGhostAdvanced("seas_mine_misc_3", {-1.43,0.39,6.39});
				AddGhostAdvanced("seas_mine_misc_4", {6.94,-0.02,0.0});
				AddGhostAdvanced("seas_mine_misc_5", {1.63,-6.16,0.0});
			elseif(m_sGfxClass=="seas_pub")then
				AddGhostAdvanced("seas_pub_misc_0", {-1.0,3.05,-0.3});
				AddGhostAdvanced("seas_pub_misc_1", {5.17,2.28,3.34});
			elseif(m_sGfxClass=="seas_staircase")then
				AddGhostAdvanced("seas_staircase_misc_0", {-2.7,6.3,-2.8});
				AddGhostAdvanced("seas_staircase_misc_1", {-2.7,2.0,0.0});
			elseif(m_sGfxClass=="seas_laboratory")then
				AddGhostAdvanced("seas_laboratory_misc_1", {-9.2,-8.4,-0.4});
			elseif(m_sGfxClass=="seas_portal_terminus" || m_sGfxClass=="seas_warpgate")then
				AddGhostAdvanced("seas_wg_misc_0", {0.0,-1.47,-5.67});	
			elseif(m_sGfxClass=="seas_small_cannon")then
				AddGhostAdvanced("seas_hq_defense_turret_top", "we");
			elseif(m_sGfxClass=="seas_great_cannon")then
				AddGhostAdvanced("seas_hq_big_cannon_rotator", "we");
				AddGhostAdvanced("seas_hq_big_cannon_cannon", 1, "we");
			elseif(m_sGfxClass.Find("_special_box")!=-1)then
				AddGhostAdvanced("special_box_misc_0", {-1.35,0.0,0.3});
				AddGhostAdvanced("special_box_misc_1", {1.35,0.0,0.3});
			endif;
			
			var ^CObjMgr pxObjMgr = ^(CClientWrap.GetObjMgr());
			if (pxObjMgr == null) then
				KLog.LogError("AddGhostAdvanced", "pxObjMgr == null");
				return;
			endif;
			
			ClearTemplatesPos();
			m_iNumGhostsInTemplate = pxObjMgr^.NumGhosts();
			m_iNumTemplates = 0;
			CClientWrap.GetObjMgr().GetObjPlaceChecker()^.EnableGridRendering(!m_bRallyPoint);

			var real fTmp;
			if(!CSettings.Get("Game/GUIOptions/BuildingRotation/Value",fTmp))then
				CSettings.Set("Game/GUIOptions/BuildingRotation/Value",0.0f);
				fTmp=0.0f;
			endif;
			fTmp-=CMultiCameraController.Get().GetGameCameraModule()^.m_fAngle;
			m_fGhostRot=fTmp;
			MoveGhost();
			ApplyGhostRot(true);
		endif;
		CWindowMgr.Get().BringWindowToTop(CClientWrap.GetDesktop(),true);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass)
		AddGhostAdvanced(p_sObjClass, {0.0,0.0,0.0}, {0.0,0.0,0.0}, 0, "", 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, vec3 p_vPosOffset)
		AddGhostAdvanced(p_sObjClass, p_vPosOffset, {0.0,0.0,0.0}, 0, "", 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, string p_sLink)
		AddGhostAdvanced(p_sObjClass, {0.0,0.0,0.0}, {0.0,0.0,0.0}, 0, p_sLink, 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, int p_iParentGhostIndex, string p_sLink)
		AddGhostAdvanced(p_sObjClass, {0.0,0.0,0.0}, {0.0,0.0,0.0}, p_iParentGhostIndex, p_sLink, 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, int p_iConstructLvl)
		AddGhostAdvanced(p_sObjClass, {0.0,0.0,0.0}, {0.0,0.0,0.0}, 0, "", p_iConstructLvl);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, vec3 p_vPosOffset, vec3 p_vRotAddDeg)
		AddGhostAdvanced(p_sObjClass, p_vPosOffset, p_vRotAddDeg, 0, "", 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, vec3 p_vPosOffset, vec3 p_vRotAddDeg, int p_iParentGhostIndex, string p_sLink)
		AddGhostAdvanced(p_sObjClass, p_vPosOffset, p_vRotAddDeg, p_iParentGhostIndex, p_sLink, 4);
	endproc;
	
	export proc void AddGhostAdvanced(string p_sObjClass, vec3 p_vPosOffset, vec3 p_vRotAddDeg, int p_iParentGhostIndex, string p_sLink, int p_iConstructLvl)
		var ^CObjMgr pxObjMgr = ^(CClientWrap.GetObjMgr());
		if(pxObjMgr == null)then
			KLog.LogError("AddGhostAdvanced","pxObjMgr == null");
			return;
		endif;

		var int iGhostIndex = pxObjMgr^.AddGhost(p_sObjClass);
		var ^CGhost pxGhost = pxObjMgr^.GetGhostIndex(iGhostIndex);
		if(pxGhost == null)then
			KLog.LogError("AddGhostAdvanced","pxGhost == null");
			return;
		endif;
		
		var vec3 vPos = {0.0,0.0,0.0};
		vPos+=p_vPosOffset;
		
		var Quat qRot; qRot.SetIdentity();
		qRot.FromVec3(p_vRotAddDeg);
		
		//Apply link PosRot to addon
		if(!p_sLink.IsEmpty())then
			var CFourCC xLink = p_sLink;
			var ^CGhost pxParentGhost = pxObjMgr^.GetGhostIndex(p_iParentGhostIndex);
			if(pxParentGhost == null)then return; endif;
			var vec3 vLinkPos = {0.0,0.0,0.0};
			var Quat qLinkRot; qLinkRot.SetIdentity();
			if(!pxParentGhost^.GetLinkPosRotWorld(xLink, vLinkPos, qLinkRot)) then
				KLog.LogError("AddGhostAdvanced","GetLinkPosRotWorld failed for link: " + p_sLink);
				return; 
			endif;
			vPos += vLinkPos;
			qRot *= qLinkRot;
		endif;

		//HACK
		//Store ghost info in name to read it later
		pxGhost^.SetName(pxGhost^.GetGhostClass() + "#" + vPos.ToString() + "#" + qRot.ToString()+ "#" + p_iParentGhostIndex.ToString() + "#" + p_sLink + "#" + p_iConstructLvl.ToString());
		//ENDHACK
		
		pxGhost^.SetPos(vPos);
		pxGhost^.SetRot(qRot);
		
		var ^CAttribs pxPlA = CGameWrap.GetClient().GetPlayer().GetAttribs();
		if(pxPlA!=null && iGhostIndex==0 && (m_sGfxClass.Find("fireplace")>=0 || m_sGfxClass.Find("headquarters")>=0)) then
			pxGhost^.SetAge(pxPlA^.GetValueInt("age"));
		endif;
		
		pxGhost^.SetConstructLevel(p_iConstructLvl);
	endproc;
	
	export proc void AddGhostTemplate()
		if (!m_bActive) then return; endif;
		if(m_bCoastal || m_bGate || m_bWall || m_bTower)then return; endif;
		if (m_iNumTemplates == -1 || m_iNumTemplates > m_avTemplatesXYMatrix.NumEntries()-1) then return; endif;

		/*TODO
		var ^CPlayer pxPlayer = ^(CGameWrap.GetClient().GetPlayer());
		if(pxPlayer==null)then
			CGameWrap.LogError("Controller.Place","Player invalid!");
			return;
		endif;
		var CTechTree xTechTree = pxPlayer^.GetPlayerTechTree();
		var int iCur=GetBuildingCount(m_sObjClass);
		var int iMax=xTechTree.GetValueI(m_sObjClass+"/max_quantity",-1);
		if((iMax>0&&(iCur>=iMax))||xTechTree.GetValueI(m_sObjClass+"/max_reached",0)==1)then
			return;
		endif;
		*/
		
		var ^CObjMgr pxObjMgr = ^(CClientWrap.GetObjMgr());
		if (pxObjMgr == null) then
			KLog.LogError("AddGhostTemplate", "pxObjMgr == null");
			return;
		endif;
		
		var int iNumGhosts = CClientWrap.GetObjMgr().NumGhosts();
		if(iNumGhosts == 0 || iNumGhosts == -1)then
			KLog.LogError("AddGhostTemplate","iNumGhosts == null");
			return;
		endif;

		//Calc save zone from first model in template
		var ^CGhost pxGhost = pxObjMgr^.GetGhostIndex(0);
		m_vTemplateSaveZone.SetX(pxGhost^.GetSize().GetX() + 1.0f);
		m_vTemplateSaveZone.SetY(pxGhost^.GetSize().GetY() + 1.0f);
			
		//Add save zone to template
		var vec3 vTemplatePos;
		vTemplatePos.SetX(m_avTemplatesXYMatrix[m_iNumTemplates].GetX() * m_vTemplateSaveZone.GetX());
		vTemplatePos.SetY(m_avTemplatesXYMatrix[m_iNumTemplates].GetY() * m_vTemplateSaveZone.GetY());

		var int i;
		for(i=0)cond(i < m_iNumGhostsInTemplate)iter(i++)do
			pxGhost = pxObjMgr^.GetGhostIndex(i);
			var CGhostInfo xGhostInfo = GetGhostInfo(pxGhost^.GetName());
			m_avPos.AddEntry(xGhostInfo.m_vPosOffset + vTemplatePos);
			AddGhostAdvanced(xGhostInfo.m_sClass, xGhostInfo.m_vPosOffset + vTemplatePos, {0.0, 0.0, 0.0}, xGhostInfo.m_iParentGhostIndex, xGhostInfo.m_sLink, xGhostInfo.m_iConstructLvl);
		endfor;
		
		MoveGhost();
		ApplyGhostRot(false);

		m_iNumTemplates++;
	endproc;

	export proc bool HandleCatchedMsg(bitset p_dwType, bitset p_dwFlags, int p_iKey, int p_iX, int p_iY)
		if(p_dwType==CInputController.MT_MouseMove)then
			HandleMsg(p_dwType, p_dwFlags, p_iKey, p_iX, p_iY);
		endif;
		return false;
	endproc;

	export proc bool HandleMsg(bitset p_dwType, bitset p_dwFlags, int p_iKey, int p_iX, int p_iY)
		if(p_dwType==CInputController.MT_Tick || p_dwType==CInputController.MT_MouseWheel || p_dwType==CInputController.MT_MouseMBtnDown || p_dwType==CInputController.MT_MouseMBtnUp || p_dwType==CInputController.MT_MouseMBtnDoubleClick)then
			return false;
		endif;

		var bool bCtrl = ((p_dwFlags&Msg.KM_CONTROL)!=0d);
		var bool bShift = ((p_dwFlags&Msg.KM_SHIFT)!=0d);
		var bool bAlt = ((p_dwFlags&Msg.KM_ALT)!=0d);

		if(m_bRotateBegin && (!bShift || !m_bLBtnHovered))then
			m_bRotateBegin=false;
		endif;

		if(m_bActive)then
			if(p_dwType==CInputController.MT_MouseLBtnDown && !m_bPlaceBegin)then
				m_vPlacement = GetPickpoint();
				m_bPlaceBegin=true;
				if((p_dwType&CInputController.MT_MouseLBtnDown)!=0d)then
					m_bLBtnHovered = true;
				endif;
			elseif((p_dwType&CInputController.MT_MouseLBtnUp)!=0d)then
				m_bLBtnHovered = false;
			elseif(p_dwType==CInputController.MT_MouseRBtnUp)then
				SetActive(false);
				return true;
			endif;

			if(!m_bWall && !m_bTower && !m_bTrap)then
				if(p_dwType==CInputController.MT_MouseMove)then
					if(!bShift && !m_bLBtnHovered)then
						m_xMousePlacement.SetX(p_iX);
						m_xMousePlacement.SetY(p_iY);
						MoveGhost();
					else
						RotateGhost(p_iX,p_iY);
						m_bRotateBegin=true;
						CWindowMgr.Get().SetMousePosNoMMove(m_xMousePlacement);
					endif;
				elseif(p_dwType==CInputController.MT_MouseLBtnUp)then
					m_vPlacement = GetPickpoint();
					var bool bPlaced=PlaceGhost(bCtrl);
					var bool bActive=!bPlaced || bCtrl;
					if(!bActive||!m_bGate)then
						SetActive(bActive);
					endif;
					m_bPlaceBegin=false;
					return true;
				endif;
			else
				if(!m_bPlaceBegin)then
					m_vPlacement = GetPickpoint();
				endif;
				if(p_dwType==CInputController.MT_MouseLBtnUp)then
					if(!bShift)then
						PlaceWall(bCtrl);
						if(!bCtrl)then
							SetActive(false);
						endif;
						m_vPlacement = GetPickpoint();
						m_bValid = false;
						m_bPlaceBegin=false;
					else
						m_vPlacement = GetPickpoint();
						m_iNumTilesHold=m_xWall.NumTiles();
					endif;
					return true;
				endif;
			endif;
			m_bValid=false;
		endif;

		var bool bReturn = m_bActive||m_bPlaceBegin;

		if(m_bPlaceBegin && p_dwType==CInputController.MT_MouseLBtnUp)then
			m_bPlaceBegin=false;
		endif;

		return bReturn;
	endproc;

	export proc bool GetActive()
		return m_bActive;
	endproc;

	export proc void SetActive(bool p_bStatus)
		var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhost();
		if(!p_bStatus || (pxGhost==null))then
			var CClntHndl xInvalid;
			m_bActive = false;
			m_bWall=false;
			m_bGate=false;
			m_bTower=false;
			m_bTrap=false;
			m_bTrap=false;
			m_bSiege=false;
			m_bInvalid=false;
			m_bCoastal=false;
			m_bPlaceBegin=false;
			m_bLBtnHovered = false;
			CClientWrap.GetObjMgr().DeleteGhost();
			m_iNumTilesHold=0;
			ClearWall();
			CClientWrap.GetObjMgr().GetObjPlaceChecker()^.EnableGridRendering(false);
			CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap()^.EnableGridRendering(false);
		else
//			m_bActive = true;
			m_vPlacement={0.0,0.0,0.0};
//			m_bPlaceBegin = false;
		endif;
		m_bValid=false;
		m_bRotateBegin = false;
//		m_bPlaceBegin=false;
		if(!p_bStatus)then
			if(!CMainController.Get().RallyUpdate())then
				CMainController.Get().WalkTargetUpdate();
			endif;
		endif;
	endproc;

	export proc void RotateGhost(int p_iX, int p_iY)
		if(m_bCoastal || m_bGate)then return; endif;
		
		if(CClientWrap.GetObjMgr().NumGhosts() <= 0)then
			KLog.LogError("RotateGhost","NumGhosts == 0");
			return;
		endif;
		
		var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(0);
		if(pxGhost == null)then
			KLog.LogError("RotateGhost","pxGhost == null");
			return;
		endif;
		var int iDiffX = (p_iX - m_xMousePlacement.GetX() + 360) % 360;
		m_fGhostRot+=Math2.DegToRad(iDiffX.ToReal());
		ApplyGhostRot(true);
	endproc;
	
	export proc void ApplyGhostRot(bool p_bClip)
		var int i, iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
		var real fRotStep=Math2.DegToRad(360.0f / m_iBuildingRotSteps.ToReal()); //-> 16 steps = 22.5°
		var vec3 vBaseGhostPos = {0.0,0.0,0.0};
		m_fGhostRot/=fRotStep;
		if(p_bClip)then
			m_fGhostRot=(m_fGhostRot+0.5f).ToInt().ToReal();
		endif;
		m_fGhostRot*=fRotStep;
		ClearTemplatesPos();
		for(i=0)cond(i<iNumGhosts)iter(i++)do
			var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(i);
			if(pxGhost == null)then
				KLog.LogError("ApplyGhostRot","pxGhost == null");
				return;
			endif;
			var CGhostInfo xGhostInfo = GetGhostInfo(pxGhost^.GetName());
			xGhostInfo.m_qAddRot.RotateZ(m_fGhostRot);
			pxGhost^.SetRot(xGhostInfo.m_qAddRot);
			if(i == 0)then
				vBaseGhostPos = pxGhost^.GetPos();
			else
				var vec3 vNewPos = Math2.RotateAroundZAxis(xGhostInfo.m_vPosOffset+vBaseGhostPos,vBaseGhostPos,-m_fGhostRot);
				pxGhost^.SetPos(vNewPos);
				
				var int iTemplate = 0;
				var int iTemplateBaseGhostIdx = 0;
				if (m_iNumGhostsInTemplate == 1) then
					m_avPos.AddEntry(vNewPos - GetPickpoint());
				else
					for(iTemplate=0)cond(iTemplate<m_iNumGhostsInTemplate)iter(iTemplate++)do
						iTemplateBaseGhostIdx = iTemplate * m_iNumGhostsInTemplate;
						if (i == iTemplateBaseGhostIdx) then
							m_avPos.AddEntry(vNewPos - GetPickpoint());
						endif;
					endfor;
				endif;
			endif;
		endfor;
	endproc;

	export proc void MoveGhost()
		var int i, iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
		var string sBaseGhostClass;

		var vec3 vCoastalNewPos = {0.0,0.0,0.0};
		var Quat qCoastalNewRot; qCoastalNewRot.SetIdentity();
		var bool bValidCoastalPos = false;
		
		for(i=0)cond(i<iNumGhosts)iter(i++)do
			var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(i);
			if(pxGhost==null)then return; endif;
			if(m_bGate)then
				CClientWrap.GetObjMgr().CreateGhost(m_sGfxClass);
				pxGhost=CClientWrap.GetObjMgr().GetGhost();
				if(pxGhost==null)then return; endif;
			endif;
			if(i == 0)then
				sBaseGhostClass = pxGhost^.GetGhostClass();
			endif;
			var vec3 vPickpoint = GetPickpoint();
			if(vPickpoint!={0.0,0.0,0.0})then
				var CGhostInfo xGhostInfo = GetGhostInfo(pxGhost^.GetName());
				if(m_bCoastal)then
					if(i == 0)then
						bValidCoastalPos = CClientWrap.GetObjMgr().GetObjPlaceChecker()^.CheckGetCoastal(sBaseGhostClass, vPickpoint, vCoastalNewPos, qCoastalNewRot);
						pxGhost^.SetPos(vCoastalNewPos);
					else
						pxGhost^.SetPos(Math2.RotateAroundZAxis(vCoastalNewPos + xGhostInfo.m_vPosOffset,vCoastalNewPos,-(2.0f * Math.ATan2(qCoastalNewRot.GetZ(), qCoastalNewRot.GetW()))));
					endif;
					pxGhost^.SetRot(qCoastalNewRot);
					if(bValidCoastalPos)then
						ColorGhosts(m_xPlaceable);
					else
						ColorGhosts(m_xNotPlaceable);
					endif;
				else
					pxGhost^.SetPos(Math2.RotateAroundZAxis(vPickpoint + xGhostInfo.m_vPosOffset,vPickpoint,-m_fGhostRot));
					if(m_bRallyPoint)then ColorGhosts(m_xPlaceable); endif;
				endif;
			endif;
			m_vPlacement=vPickpoint;
		endfor;
	endproc;
	
	proc CGhostInfo GetGhostInfo(string p_sGhostName)
		var CGhostInfo xGhostInfo;
		var array string asGhostInfo;

		if(p_sGhostName.IsEmpty() || p_sGhostName.Find("#")==-1)then
			KLog.LogError("GetGhostInfo","GhostName empty");
			return xGhostInfo;
		endif;

		p_sGhostName.Split(asGhostInfo, "#", false);
		if(asGhostInfo.NumEntries() < 5) then
			KLog.LogError("GetGhostInfo","asGhostInfo < 5");
			return xGhostInfo;
		endif;
		
		xGhostInfo.m_sClass = asGhostInfo[0];
		
		xGhostInfo.m_vPosOffset.FromString(asGhostInfo[1]);
		
		var string sRot = asGhostInfo[2];
		var array string asXYZW;
		sRot.Split(asXYZW, " ", false);
		if(asXYZW.NumEntries() < 4) then
			KLog.LogError("GetGhostInfo","asXYZW < 4");
			return xGhostInfo;
		endif;
		xGhostInfo.m_qAddRot.SetXYZW(asXYZW[0].ToReal(),asXYZW[1].ToReal(),asXYZW[2].ToReal(),asXYZW[3].ToReal());

		xGhostInfo.m_iParentGhostIndex = asGhostInfo[3].ToInt();
		xGhostInfo.m_sLink = asGhostInfo[4];
		xGhostInfo.m_iConstructLvl = asGhostInfo[5].ToInt();
		
		return xGhostInfo;
	endproc;

	proc void CheckPlacebility()
		var int iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
		if(iNumGhosts <= 0)then
			KLog.LogError("CheckPlacebility","NumGhosts == 0");
			return;
		endif;
		
		var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(0);
		
		if(pxGhost==null)then return; endif;
		if(m_bGate)then
			ClearWall();
			var ^CWallMapCln pxWM=CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap();
			var bool bFound=pxWM^.CalcWall(-1,m_sClass,m_vPlacement,m_vPlacement,m_xWall);
			if(bFound)then
				pxWM^.PreviewWall(m_xWall,m_sGfxClass,m_xPlaceable);
				ColorGhosts(m_xPlaceable);
				return;
			else
				ColorGhosts(m_xNotPlaceable);
				return;
			endif;
		elseif(m_bCoastal)then
			//None
		else
			var int i, iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
			for(i=0)cond(i<iNumGhosts)iter(i+=m_iNumGhostsInTemplate)do
				if(i==0)then continue; endif;
				pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(i);
				if((CClientWrap.GetObjMgr().GetObjPlaceChecker()^.Check(UseSmallerGfx(pxGhost),pxGhost^.GetPos(),pxGhost^.GetRotation())) || m_bRallyPoint)then
					ColorGhosts(m_xPlaceable, i);
				else
					ColorGhosts(m_xNotPlaceable, i);
				endif;
			endfor;
			pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(0);
			if((CClientWrap.GetObjMgr().GetObjPlaceChecker()^.Check(UseSmallerGfx(pxGhost),pxGhost^.GetPos(),pxGhost^.GetRotation())) || m_bRallyPoint)then
				ColorGhosts(m_xPlaceable, 0);
			else
				ColorGhosts(m_xNotPlaceable, 0);
			endif;
		endif;
	endproc;
	
	proc void ColorGhosts(color p_xColor, int iTemplate)
		//TODO
		var int i, iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
		for(i=0)cond(i<iNumGhosts)iter(i++)do
			var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(i);
			if(pxGhost==null)then return; endif;
			if(i==iTemplate)then
				pxGhost^.SetColored(true);
				pxGhost^.SetColor(p_xColor);
			endif;
		endfor;
	endproc;

	proc void ColorGhosts(color p_xColor)
		var int i, iNumGhosts=CClientWrap.GetObjMgr().NumGhosts();
		for(i=0)cond(i<iNumGhosts)iter(i++)do
			var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhostIndex(i);
			if(pxGhost==null)then return; endif;
			pxGhost^.SetColored(true);
			pxGhost^.SetColor(p_xColor);
		endfor;
	endproc;

	export proc void AddWall()
		var ^CPlayer pxPlayer = ^(CGameWrap.GetClient().GetPlayer());
		var CTechTree xTechTree = pxPlayer^.GetPlayerTechTree();
		var vec3 vPickpoint = GetPickpoint();
		if(vPickpoint=={0.0,0.0,0.0})then return; endif;
		var string sObjClass = m_sGfxClass;
		var ^CWallMapCln pxWM=CClientWrap.GetObjMgr().GetObjPlaceChecker()^.GetWallMap();
		var bool bFound=pxWM^.CalcWall(-1,m_sClass,m_vPlacement,vPickpoint,m_xWall,m_iNumTilesHold);
		/// todo: check resources for all tiles present
		pxWM^.EnableGridRendering(bFound);
		if(bFound)then
			var ^CGhost pxGhost=pxWM^.PreviewWall(m_xWall,m_sGfxClass,{{0,255,0,255}},m_iResNumPossible,{{255,255,0,255}});
			pxWM^.ShowPlacement(m_xWall,vPickpoint);
			if(pxGhost!=null)then
				if(m_sGfxClass=="aje_tesla_tower")then
					var CFourCC xLink = "we";
					var int iLinkID = pxGhost^.GetLinkID(xLink);
					pxGhost^.LinkGfx(iLinkID, "aje_tesla_tower_canon");
				elseif(m_sGfxClass=="hu_large_tower_upgrade")then
					var CFourCC xLink = "we";
					var int iLinkID = pxGhost^.GetLinkID(xLink);
					pxGhost^.LinkGfx(iLinkID, "Hu_Large_Tower_Upgrade_Balista");
				//addition by Henry to improve towers ghost objects by adding the turret
				elseif(m_sGfxClass=="seas_turret_tower")then
					var CFourCC xLink = "we";
					var int iLinkID = pxGhost^.GetLinkID(xLink);
					pxGhost^.LinkGfx(iLinkID, "seas_turret");
				elseif(m_sGfxClass=="ninigi_small_tower")then
					var CFourCC xLink = "we";
					if(xTechTree.GetValueB("/Objects/Ninigi/InventObjects/tower_sordes_upgrade/invented",false))then
						var int iLinkID = pxGhost^.GetLinkID(xLink);
						pxGhost^.LinkGfx(iLinkID, "ninigi_small_tower_upgrade");
					endif;
				elseif(m_sGfxClass=="ninigi_large_tower")then
					var CFourCC xLink = "Proj";
					var int iLinkID = pxGhost^.GetLinkID(xLink);
					pxGhost^.LinkGfx(iLinkID, "ninigi_rocket_ramp_top");
					//HACK by Henry to enable telescope towers ghost to play animation
				/*elseif(m_sGfxClass=="ninigi_telescope_tower")then
					pxGhost^.SetAnim("idle",3,0);*/
				endif;
			endif;
		endif;
	endproc;

	export proc void PlaceWall(bool p_bQueued)
		var ^CObjList pxSel = ^(CClientWrap.GetSelection());
		CEvt_PlaceWall.Send(m_xWall,pxSel^,p_bQueued);
		ClearWall();
	endproc;

	export proc bool PlaceGhost(bool p_bQueued)
		var ^CGhost pxGhost = CClientWrap.GetObjMgr().GetGhost();
		var ^CObjList pxSel = ^(CClientWrap.GetSelection());
		if(pxGhost==null)then return false; endif;
		if(m_bRallyPoint)then
			m_bRallyPoint = false;
			var CClntHndl xInvalid;
			var CClntHndl xHitObj=CClientWrap.GetObjMgr().GetHitObj();
			if(CClientWrap.GetForestMgr().HitTree(m_vPlacement) && CGameWrap.IsPosFOWVisible(m_vPlacement))then
			elseif(xHitObj.IsValid())then
				var ^CObj pxHitObj=xHitObj.GetObj();
				if(pxHitObj!=null)then
					pxHitObj=pxHitObj^.GetGroupedParentObj();
				endif;
				if(pxHitObj!=null)then
					if(pxHitObj^.GetType()=="TREE"||pxHitObj^.GetType()=="WOOD"||pxHitObj^.GetType()=="FOOD"||pxHitObj^.GetType()=="FRUI"||pxHitObj^.GetType()=="STON")then
						xInvalid=xHitObj;
					elseif(!pxHitObj^.IsEnemy())then
						xInvalid=xHitObj;
					endif;
				endif;
			endif;
			var int i,iC=pxSel^.NumEntries();
			for(i=0)cond(i<iC)iter(i++)do
				CEvt_GamePlayCommand.Send(pxSel^[i], "SetRallyPoint", xInvalid, m_vPlacement, m_vPlacement.ToString());
			endfor;
			
			//KLog.LogSpam("uisound","PlaceGhost "+m_sClass+" "+m_sAction);
			CGameInputController.ms_xUISoundMgr.PlayRallypointFeedbackSound(m_vPlacement);
			return true;
		endif;

		if(m_vPlacement!={0.0,0.0,0.0})then
			if(m_bGate)then
				var ^CObjList pxSel = ^(CClientWrap.GetSelection());
				CEvt_PlaceWall.Send(m_xWall,pxSel^,p_bQueued);
				ClearWall();
//				CClientWrap.GetObjMgr().DeleteGhost();
				return true;
			elseif(m_bCoastal)then

				var vec3 vNewPos = {0.0,0.0,0.0};
				var Quat qNewRot; qNewRot.SetIdentity();
				var vec3 vPickpoint = GetPickpoint();

				if(CClientWrap.GetObjMgr().GetObjPlaceChecker()^.CheckGetCoastal(pxGhost^.GetGhostClass(), vPickpoint, vNewPos, qNewRot))then
					pxGhost^.SetPos(vNewPos);
					pxGhost^.SetRot(qNewRot);
					CEvt_PlaceObj.Send(m_sClass+";"+m_sTribe+";"+m_sAction,m_vPlacement,pxGhost^.GetRotation(),pxSel^,p_bQueued);
					return true;
				else
					pxGhost^.SetPos(vPickpoint);
					ColorGhosts(m_xNotPlaceable);
					CFeedback.Print(CLocalizer.Get().Translate("_PlaceController_Object"));
					CGameInputController.ms_xUISoundMgr.PlayFeedbackSoundEvent("ui_click_error","",CSoundMgrCln.SOUND_VOLTRACK_GLOBAL);
					CGameInputController.ms_xUISoundMgr.PlayFeedbackSoundEvent("voice_click_error","",CSoundMgrCln.SOUND_VOLTRACK_SPEECH_ACK);
					return false;
				endif;
			else
				if(!CClientWrap.GetObjMgr().GetObjPlaceChecker()^.Check(UseSmallerGfx(pxGhost),m_vPlacement,pxGhost^.GetRotation()))then
					CFeedback.Print(CLocalizer.Get().Translate("_PlaceController_Object"));
					CGameInputController.ms_xUISoundMgr.PlayFeedbackSoundEvent("ui_click_error","",CSoundMgrCln.SOUND_VOLTRACK_GLOBAL);
					CGameInputController.ms_xUISoundMgr.PlayFeedbackSoundEvent("voice_click_error","",CSoundMgrCln.SOUND_VOLTRACK_SPEECH_ACK);
					return false;
				endif;
	
				CEvt_PlaceObj.Send(m_sClass+";"+m_sTribe+";"+m_sAction,m_vPlacement,pxGhost^.GetRotation(),pxSel^,p_bQueued);
				var int i;
				for(i=0)cond(i<m_avPos.NumEntries())iter(i++)do
					CEvt_PlaceObj.Send(m_sClass+";"+m_sTribe+";"+m_sAction,m_vPlacement+m_avPos[i],pxGhost^.GetRotation(),pxSel^,p_bQueued);
				endfor;
				return true;
			endif;
		endif;
		return false;
	endproc;

	proc vec3 GetPickpoint()
		var vec3 vPos=CClientWrap.GetScapeMgr().GetPickPoint();
		if(m_bCoastal)then
			vPos.SetZ(Math.Max(vPos.GetZ(),CClientWrap.GetScapeMgr().GetSeaLevel()));
		endif;
		return vPos;
	endproc;

//Henry: the fake buildings names need to be identical at the same procedure in ServerApp.usl
	export proc string UseSmallerGfx(^CGhost p_pxGhost) //Henry: HACK for buildings get the placemanager think they are smaller ^^
		var string sGfx = "dummy";
		if(p_pxGhost!=null)then
			sGfx = p_pxGhost^.GetGhostClass();
		endif;
		if(sGfx=="seas_big_tent")then
			sGfx = "ninigi_engineer";
		elseif(sGfx=="seas_small_tent")then
			sGfx = "hu_weapons_smith";
		elseif(sGfx=="seas_mine")then
			sGfx = "hu_weapons_smith";
		elseif(sGfx=="seas_barracks")then
			sGfx = "hu_arena";
		elseif(sGfx=="seas_fortress")then
			sGfx = "ninigi_temple";
		elseif(sGfx=="seas_garage")then
			sGfx = "hu_arena";
		elseif(sGfx=="seas_great_cannon")then
			sGfx = "hu_arena";
		elseif(sGfx=="seas_final_boss")then
			sGfx = "hu_weapons_smith";
		elseif(sGfx=="seas_laboratory")then
			sGfx = "hu_arena";
		elseif(sGfx=="seas_steelwork")then
			sGfx = "ninigi_dojo";
		elseif(sGfx=="aje_amazon_temple")then
			sGfx = "aje_temple";
		elseif(sGfx=="hu_mine")then
			sGfx = "hu_weapons_smith";
		elseif(sGfx=="aje_small_farm"||sGfx=="aje_huge_farm")then
			sGfx = "aje_medium_farm";
		endif;
		return sGfx;
	endproc;

endclass;

class CGhostInfo

	export var string m_sClass;
	export var vec3 m_vPosOffset;
	export var Quat m_qAddRot;
	export var int m_iParentGhostIndex;
	export var string m_sLink;
	export var int m_iConstructLvl;

	export constructor()
		m_sClass = "";
		m_vPosOffset = {0.0,0.0,0.0};
		m_qAddRot.SetIdentity();
		m_iParentGhostIndex = 0;
		m_sLink = "";
		m_iConstructLvl = 4;
	endconstructor;

endclass;
