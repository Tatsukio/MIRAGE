class CChatEdit inherit CEdit

	export proc bool OnActivate()
		if(GetText()==CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then
			SetText("");
		endif;
		return super.OnActivate();
	endproc;

endclass;

class CChat

	const string ZIDELL_CDKH="44A58D488C35F64F310981D62AD3619A";
	const string KR1S1M_CDKH="98B4FFFF2789736B98A3C46020C586DA";
	const string PWFAN_CDKH="E3B4747D28982FCB8812C157FBBB602F";
	
	export static proc bool CheckDevStatus(string p_sUserCDKeyHash)
		return (p_sUserCDKeyHash==ZIDELL_CDKH ||	
				p_sUserCDKeyHash==KR1S1M_CDKH || 
				p_sUserCDKeyHash==PWFAN_CDKH);
	endproc;

	export static proc bool Compare(string p_sSource, string p_sCompare)
		p_sSource.MakeLower();
		p_sCompare.MakeLower();
		while(p_sSource.GetLength()>0 && p_sCompare.GetLength()>0)do
			if(p_sSource.GetAt(0)!=p_sCompare.GetAt(0))then
				return false;
			endif;
			p_sSource.Delete(0);
			p_sCompare.Delete(0);
		endwhile;
		return true;
	endproc;

	export static proc ^CLevelInfo.CPlayerSlot GetSlotFromName(string p_sName)
		var CPlayerSlotID xSlotID = CPlayerSlotID.First();
		for(xSlotID)cond(xSlotID<=CPlayerSlotID.Last())iter(xSlotID.Incr())do
			var ^CLevelInfo.CPlayerSlot pxPlayerSlot = ^(CGameWrap.GetClient().GetLevelInfo().GetPlayerSlot(xSlotID));
			if(pxPlayerSlot==null || pxPlayerSlot^.IsFree())then continue; endif;
			var string sName = CLocalizer.Get().Translate(pxPlayerSlot^.GetName());
			if(p_sName != sName)then continue; endif;
			return pxPlayerSlot;
		endfor;
		return null;
	endproc;
	
	export static proc string GetUserName()
		var string sUser;
		var ^CUserProfileList pxList=^(CClientWrap.GetUserProfileList());
		if(pxList!=null)then
			sUser=pxList^.GetCurrentProfile()^.GetUserName();
			if(sUser=="")then
				sUser=Windows.GetUserName();
				CSettings.Set("Game/PlayerName",sUser);
			endif;
		endif;
		return sUser;
	endproc;
	
	export static proc bool IsValid(string p_sMsg)
		var string sMsg = p_sMsg;
		if(sMsg.GetLength()<=0)then return false; endif;
		if(sMsg=="" || sMsg == CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then return false; endif;
		return true;
	endproc;
	
	export static proc bool IsCommand(string p_sMsg)
		var string sMsg = p_sMsg;
		if(sMsg.GetLength()==1 && sMsg.GetAt(0)=='/')then return false; endif;
		if(sMsg.GetAt(0)=='/' && sMsg.GetAt(1)=='/')then return false; endif;
		if(sMsg.GetAt(0)=='/')then
			sMsg.Delete(0);
			var int iIndex = sMsg.Find(" ");
			if(iIndex==-1)then
				iIndex = sMsg.GetLength();
			endif;
			var string sCommand = sMsg.Left(iIndex);
			sMsg.Delete(0,iIndex+1);
			if(Compare("who",sCommand))then
				CmdWho(sMsg);
			else
				FeedbackMsg(CLocalizer.Get().Translate("_UI_ChatWind_Msg_UnknownCommand")+sCommand);
				return false;
			endif;
		endif;
		return false;
	endproc;
	
	export static proc void CmdWho(string p_sMsg)
		var string sSys = "&System";
		if(p_sMsg=="Schlachtermanni")then
			FeedbackMsg("He's so f*****g 80's!");
		elseif(p_sMsg=="Lowtec")then
			FeedbackMsg(": He's so f*****g cool!");
		elseif(p_sMsg=="Beulchen")then
			FeedbackMsg(": She's so f*****g uber!");
		elseif(p_sMsg=="Deca")then
			FeedbackMsg(": Beulchen says hello to her friend Deca :)");
		elseif(p_sMsg=="TechGirl")then
			FeedbackMsg(": TechGirl's better then all of them!");
		elseif(p_sMsg=="Zidell")then
			FeedbackMsg(": He's a non-binary bool variable!");
		else
			FeedbackMsg(CLocalizer.Get().Translate("_UI_ChatWind_Msg_Playernamenotfound"));
		endif;
	endproc;
	
	export static proc void FeedbackMsg(string p_sMsg)
		CFeedback.Print(CNewsTickerData.NTT_Chat, "&System" + p_sMsg);
	endproc;
endclass;


//---------------------------------------------------------
// IngameChat-Bar
//---------------------------------------------------------

class CIngameChatBar inherit CStaticCtrlEx

	var ^CEdit				m_pxInputEdit;
	var ^CButton			m_pxSendButton;
	var ^CDropList			m_pxInputDropList;
	var ^CLevelInfo			m_pxCurrentLevel;

	export constructor(string p_sReciever)
		if(!InitFromResource("UI/ChatWindow","IngameChatWindow"))then
			CGameWrap.LogError("UI.IngameChatWindow","InitFromResource failed");
			return;
		endif;
		
		SetSize(700 * CGameInst.ms_iUIScaleFactor,80 * CGameInst.ms_iUIScaleFactor);
		SetPos(CClientWrap.GetDesktop()^.GetWidth()/2-GetWidth()/2,(CClientWrap.GetDesktop()^.GetHeight()-(CClientWrap.GetDesktop()^.GetHeight().ToReal()/3.5).ToInt())-GetHeight()/2);
		SetFrame(true);
		SetFrameType("Custom1");
		AddAccelerator("ESCAPE,NONE,Exit");
		AddAccelerator("RETURN,NONE,Send");
		SetCmd("Exit", Exit );
		SetCmd("Send", OnSend );

		m_pxInputEdit=cast<CEdit>(GetControl("InputEdit"));
		m_pxInputEdit^.SetText("");
		m_pxInputEdit^.SetTextLimit(300);
		m_pxInputEdit^.SetMultiLine(true);
		
		m_pxSendButton=cast<CButton>(GetControl("SendButton"));
		m_pxSendButton^.m_xOnClick=OnSend;
		m_pxInputDropList=cast<CDropList>(GetControl("InputDropList"));
		m_pxInputDropList^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_ToAll"));
		m_pxInputDropList^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_ToAllies"), CGameInst.GetBitmapMgr().GetBlendBitmap("9 ,layer info_armor")^.GetBitmap());
		m_pxInputDropList^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_ToEnemies"), CGameInst.GetBitmapMgr().GetBlendBitmap("9 ,layer info_attack")^.GetBitmap());
		m_pxCurrentLevel = ^(CGameWrap.GetClient().GetLevelInfo());
		if(m_pxCurrentLevel!=null)then
			var CPlayerSlotID xOwnPlayerSlotID = CGameWrap.GetClient().GetPlayerSlotID();
			var CPlayerSlotID xSlotID = CPlayerSlotID.First();
			for(xSlotID)cond(xSlotID<=CPlayerSlotID.Last())iter(xSlotID.Incr())do
				if(xSlotID==xOwnPlayerSlotID)then continue; endif;
				var ^CLevelInfo.CPlayerSlot pxPlayerSlot = ^(m_pxCurrentLevel^.GetPlayerSlot(xSlotID));
				if(pxPlayerSlot==null || pxPlayerSlot^.IsFree())then continue; endif;
				var string sBlend="30 ,fill TeamCol "+pxPlayerSlot^.GetColor().ToString()+" 4 "+(20 * CGameInst.ms_iUIScaleFactor).ToString()+" "+(20 * CGameInst.ms_iUIScaleFactor).ToString();
				if(xSlotID == CPlayerSlotID.First())then
					sBlend+=",layer info_player_winner";
				endif;
				var CBitmapPtr pxBmp=CGameInst.GetBitmapMgr().GetBlendBitmap(sBlend)^.GetBitmap();
				m_pxInputDropList^.AddItem(CLocalizer.Get().Translate(pxPlayerSlot^.GetName()), pxBmp);
			endfor;
		endif;

		if(p_sReciever=="All")then
			m_pxInputDropList^.Select(0);
		elseif(p_sReciever=="Allies")then
			m_pxInputDropList^.Select(1);
		elseif(p_sReciever=="Enemies")then
			m_pxInputDropList^.Select(2);
		else
			m_pxInputDropList^.Select(m_pxInputDropList^.FindItem(p_sReciever));
		endif;
	endconstructor;
	
	export destructor()
	enddestructor;
	
	export proc bool SetEditControlActive()
		CWindowMgr.Get().BringWindowToTop(m_pxInputEdit,true);
		return(true);
	endproc;
	
	proc bool Exit()
		m_pxInputEdit^.SetText("");
		Destroy();
		return(true);
	endproc;
	
	proc bool OnSend()
		if(m_pxCurrentLevel==null)then return false; endif;
		var string sMsg = m_pxInputEdit^.GetText();
		var int iTime = CTimeMgr.Get().GetSecondsSinceGameStart().ToInt() + CMirageClnMgr.Get().GetSpan();
		var string sTimeStamp = "[" + Math2.GetTimeString(iTime) + "] ";
		if(!CChat.IsValid(sMsg) || CChat.IsCommand(sMsg))then
		elseif(m_pxInputDropList^.GetSelectedItem()==1)then
			GroupMsg(2, ">MsgAllies<", sTimeStamp, sMsg);
			CFeedback.Print(CNewsTickerData.NTT_Chat, CLocalizer.Get().Translate("_UI_ChatWind_Msg_ToAllies") + sTimeStamp + sMsg);
		elseif(m_pxInputDropList^.GetSelectedItem()==2)then
			GroupMsg(0, ">MsgEnemies<", sTimeStamp, sMsg);
			CFeedback.Print(CNewsTickerData.NTT_Chat, CLocalizer.Get().Translate("_UI_ChatWind_Msg_ToEnemies") + sTimeStamp + sMsg);
		elseif(m_pxInputDropList^.GetSelectedItem()>2)then
			DirectMsg(CChat.GetSlotFromName(m_pxInputDropList^.GetSelectedItemAsString()), ">MsgWhisper<", sTimeStamp, sMsg);
			CFeedback.Print(CNewsTickerData.NTT_Chat, CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_Youtold_A") + m_pxInputDropList^.GetSelectedItemAsString() + CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_Youtold_B") + sTimeStamp + sMsg);
		else
			CEvt_ChatMsg.Send(": " + sTimeStamp + sMsg);
		endif;
		Exit();
		return(true);
	endproc;
	
	export proc void DirectMsg(^CLevelInfo.CPlayerSlot p_xSlot, string p_sType, string p_sTimeStamp, string p_sMsg)
		if(p_xSlot==null || p_xSlot^.IsFree())then return; endif;
		if(p_xSlot^.IsAIPlayer())then
			var string sAISlot = "AI" + p_xSlot^.GetPlayerSlotID().ToString() + ": ";
			CEvt_AiGenericStringEvent.Send(CGameWrap.GetClient().GetPlayerID().ToString()+" "+ sAISlot + p_sMsg);
		else
			if(!CGameWrap.GetClient().HasValidPlayerSlot())then
				CEvt_ChatMsg.Send(p_sTimeStamp + p_sType + p_sMsg);
			else
				CEvt_ChatMsg.Send(p_sTimeStamp + p_sType + p_sMsg,01h<<p_xSlot^.GetPlayerSlotID());
			endif;
		endif;
	endproc;

	export proc void GroupMsg(int p_iDiplOpinion, string p_sType, string p_sTimeStamp, string p_sMsg)
		if(!CChat.IsValid(p_sMsg))then return; endif;
		var ^CDiplomacyClnMgr pxDiplMgr = ^(CGameWrap.GetDiplomacyMgr());
		if(pxDiplMgr == null)then return; endif;

		var CPlayerSlotID xOwnPlayerSlotID = CGameWrap.GetClient().GetPlayerSlotID();
		var CPlayerSlotID xSlotID;

		for(xSlotID=CPlayerSlotID.First())cond(xSlotID<=CPlayerSlotID.Last())iter(xSlotID.Incr())do
			if(xSlotID==xOwnPlayerSlotID)then continue; endif;
			var ^CLevelInfo.CPlayerSlot pxPlayerSlot = ^(m_pxCurrentLevel^.GetPlayerSlot(xSlotID));
			if(pxPlayerSlot==null || pxPlayerSlot^.IsFree())then continue; endif;
			if(pxDiplMgr^.GetMyOpinion(pxPlayerSlot^.GetOwner())!=p_iDiplOpinion)then continue; endif;
			DirectMsg(pxPlayerSlot, p_sType, p_sTimeStamp, p_sMsg);
		endfor;
	endproc;
endclass;



//---------------------------------------------------------
// IngameChat - Output
//---------------------------------------------------------
class CIngameChatOutput inherit CPane

	static var int			ms_iCurMsgs;
	var array int			m_aiDurTimerQueue;
	var string				m_sMsgText;
	var ^CStaticCtrl  		m_pxMsg;

	export constructor()
		SetPos(5 * CGameInst.ms_iUIScaleFactor, 35 * CGameInst.ms_iUIScaleFactor);
		SetSize(400 * CGameInst.ms_iUIScaleFactor, 20 * CGameInst.ms_iUIScaleFactor);
		SetVisible(true);
		SetTransparent(true);
		var color xBack = {{ 0,0,0,125 }};
		SetColor(xBack);
		var color xCol = {{ 255,255,255,255 }};
		CClientWrap.GetDesktop()^.AddChild(this, "ChatOut");
		SetVisible(false);
		m_pxMsg = new CStaticCtrl();
		m_pxMsg^.SetPos(5 * CGameInst.ms_iUIScaleFactor, 0);
		m_pxMsg^.SetSize(385 * CGameInst.ms_iUIScaleFactor, 20 * CGameInst.ms_iUIScaleFactor);
		m_pxMsg^.SetBackground(false);
		m_pxMsg^.SetSuitAlpha(true);
		m_pxMsg^.SetTransparent(true);
		m_pxMsg^.SetTextColor(xCol);
		m_pxMsg^.SetTextAlign(8);
		m_xOnTimer = OnTimer;
		AddChild(m_pxMsg);
		m_pxMsg^.SetAlwaysOnTop(true);
	endconstructor;
	
	export destructor()
		delete m_pxMsg;
	enddestructor;

	export proc void OnRcvdMsg(string p_sMsg)
		SetAlwaysOnTop(true);
		SetVisible(true);
		m_pxMsg^.SetVisible(true);
		CreateNewEntry(p_sMsg);
	endproc;

	proc void CreateNewEntry(string p_sMsg)
		if(p_sMsg == "")then return; endif;
		var string sTmp = p_sMsg + "\n" + m_sMsgText;
		m_sMsgText = sTmp;
		m_sMsgText.TrimRight('\n');
		m_aiDurTimerQueue.AddEntry(SetTimer ( 8000, false ));
		m_pxMsg^.SetText(m_sMsgText);
		++ms_iCurMsgs;
		SetSize(400 * CGameInst.ms_iUIScaleFactor, ms_iCurMsgs * 20 * CGameInst.ms_iUIScaleFactor);
		m_pxMsg^.SetSize(385 * CGameInst.ms_iUIScaleFactor, 20*ms_iCurMsgs * CGameInst.ms_iUIScaleFactor);
	endproc;

	proc bool OnTimer(int p_iID)
		// Process the DurationTimer-Tick
		if( m_aiDurTimerQueue.NumEntries() >= 0)then
			if( p_iID == m_aiDurTimerQueue[0] )then
				m_aiDurTimerQueue.DeleteEntry(0);
				var int iPos = m_sMsgText.FindRev('\n');
				if(iPos == -1)then
					m_sMsgText = "";
					m_pxMsg^.SetText(m_sMsgText);
					ms_iCurMsgs = 0;
					SetVisible(false);
					return true;
				endif;
				m_sMsgText.Delete(iPos, 99999);
				m_pxMsg^.SetText(m_sMsgText);
				--ms_iCurMsgs;
				if(m_sMsgText == "")then SetVisible(false); endif;
				SetSize(400 * CGameInst.ms_iUIScaleFactor, ms_iCurMsgs * 20 * CGameInst.ms_iUIScaleFactor);
				m_pxMsg^.SetSize(385 * CGameInst.ms_iUIScaleFactor, 20 * ms_iCurMsgs * CGameInst.ms_iUIScaleFactor);
			endif;
		endif;
		return true;
	endproc;

	export proc void Activate()
		//L CGameWrap.SetProcRefOnChatMsg(OnRcvdMsg);
	endproc;

endclass;



//---------------------------------------------------------
// NewMultiPlayerPreGameWindowChat - LobbyChat
//---------------------------------------------------------
class CNewChatWindow inherit CStateUIWnd
	
	var ^CUIFloatBox								m_pxChatList;
	var ^CChatEdit									m_pxChatEdit;
	var ^CDropList									m_pxFilter;
	var ^CCheckBox									m_pxAutoScroll;
	var bool										m_bScrollToEnd;
	var string										m_sOwnName;
	var int											m_iLineHeightEx;
	var array ^CPlayerInfoSlot						m_apxPlayerList;
	var int											m_iUpdateTimer;
	var int											m_iLastColor;
	var string										m_sChatMessages;
	
	var array string								m_asPlayerNames;
	
	var int											m_iTimerCheat;
	var int											m_iCounter;
	var int											m_iCounterMax;
	var ^CStaticCtrlEx								m_pxCheat;
	var string										m_sCheat;
	var int											m_iMaxCheatLength;
	var bool										m_bCheatWarningInited;

	export constructor()
		if(!InitFromResource("UI/ChatWindow","NewChatWindow"))then
			CGameWrap.LogError("UI.ChatWindow","InitFromResource failed");
			return;
		endif;
		CGameWrap.UnSetProcRefOnChatMsg();
		
		SetCaption(CLocalizer.Get().Translate("_UI_ChatWind_Title_Chat"));
		
		m_pxChatList=cast<CUIFloatBox>(GetControl("ChatList"));
		m_pxChatList^.SetBackground(true);
		m_pxChatList^.SetFrame(true);
		m_pxChatList^.SetContentIndentX(4);
		m_pxChatEdit=cast<CChatEdit>(GetControl("ChatEdit"));
		m_pxChatEdit^.SetText(CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"));
		m_pxChatEdit^.SetMultiLine(false);
		m_pxChatEdit^.SetReadOnly(CMultiPlayerClientMgr.Get().GetSkirmishMode());
		m_pxChatEdit^.SetTextLimit(150);
		m_pxChatEdit^.m_xOnUpdate=OnUpdateEdit;
		
		var ^CButton pxSendButton=cast<CButton>(GetControl("SendButton"));
		pxSendButton^.SetTextAlign(0);
		pxSendButton^.SetTextIndent(8);
		pxSendButton^.m_xOnClick=OnSendText;

		m_pxFilter=cast<CDropList>(GetControl("FilterDropList"));
		m_pxFilter^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_SendToAll"));
		m_pxFilter^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_SendToAllies"));
		m_pxFilter^.AddItem(CLocalizer.Get().Translate("_UI_ChatWind_Option_SendToEnemies"));
		
		CGameWrap.SetProcRefOnChatMsg(OnChatMsg);
		CGameInst.SetChatWindow(this);
		m_sOwnName=CChat.GetUserName();
		
		CMultiPlayerClientMgr.Get().GetPlayerList(m_apxPlayerList,true);
		if(m_apxPlayerList.NumEntries()>0)then
			var int i,iC = m_apxPlayerList.NumEntries();
			for(i=0)cond(i<iC)iter(i++)do
				var bool bCustomName=CMirageClnMgr.Get().GetCustomMapPlayerSettingBool(CMirageClnMgr.Get().GetMapName(),m_apxPlayerList[i]^.GetHeadQuater(),"Name");
				var bool bMatch=(m_apxPlayerList[i]!=null && m_apxPlayerList[i]^.GetName() != m_sOwnName);
				if(bCustomName)then
					bMatch=(m_apxPlayerList[i]!=null && CLocalizer.Get().Translate(m_apxPlayerList[i]^.GetName()) != m_sOwnName);
				endif;
				if(bCustomName&&bMatch)then
					m_pxFilter^.AddItem(CLocalizer.Get().Translate(m_apxPlayerList[i]^.GetName()));
				elseif(!bCustomName&&bMatch)then
					m_pxFilter^.AddItem(m_apxPlayerList[i]^.GetName());
				endif;
			endfor;
		endif;
		
		begin LoadCustomPlayerNames;
			var int i,iC=CLocalizer.Get().NumKeys();
			for(i=0)cond(i<iC)iter(i++)do
				var string sKey;
				if(CLocalizer.Get().GetKey(i,sKey))then
					if(sKey=="_PN_SYSTEM")then continue; endif;
					if(sKey.Left(4)=="_PN_")then
						m_asPlayerNames.AddEntry(sKey);
					endif;
				endif;
			endfor;
		end LoadCustomPlayerNames;

		m_iUpdateTimer=SetTimer(200,true);
		OnTimer(m_iUpdateTimer);
		m_iLastColor=1;
		m_sChatMessages="";
		m_bCheatWarningInited=false;
		m_pxAutoScroll=cast<CCheckBox>(GetControl("AutoScroll"));
		m_pxAutoScroll^.SetChecked(1);
		m_pxAutoScroll^.m_xOnStateChange=OnChangeAutoScroll;
		m_bScrollToEnd=true;
	endconstructor;
	
	export destructor()
		CGameWrap.UnSetProcRefOnChatMsg();
		CGameInst.SetChatWindow(null);
		UnsetTimer(m_iUpdateTimer);
		UnsetTimer(m_iTimerCheat);
		m_bCheatWarningInited=false;
	enddestructor;
	
	export proc bool UpdatePlayerList()
		begin UpdateUserName;
			var ^CLevelInfo.CPlayerSlot pxPlayerSlot = ^(CGameWrap.GetClient().GetLevelPreview().GetPlayerSlot(CGameWrap.GetClient().GetPlayerSlotID()));
			if(CMirageClnMgr.Get().GetCustomMapPlayerSettingBool(CMirageClnMgr.Get().GetMapName(),pxPlayerSlot^.GetHeadQuater(),"Name"))then
				m_sOwnName=pxPlayerSlot^.GetName();
			else
				m_sOwnName=CChat.GetUserName();
			endif;
		end UpdateUserName;
		
		var array ^CPlayerInfoSlot apxList;
		CMultiPlayerClientMgr.Get().GetPlayerList(apxList,true);
		
		var int j, jC = m_pxFilter^.NumItems();
		var int i, iC = apxList.NumEntries();
		var bool bIsInList;
		for(j=3)cond(j<jC)iter(j++)do
			bIsInList=false;
			for(i=0)cond(i<iC)iter(i++)do
				if(m_pxFilter^.GetItem(j) != CLocalizer.Get().Translate(apxList[i]^.GetName()))then continue; endif;
				bIsInList=true;
			endfor;
			if(!bIsInList)then
				m_pxFilter^.DeleteItem(j);
				j--;
				jC--;
			endif;
		endfor;
	
		if(apxList.NumEntries() <= (m_pxFilter^.NumItems() - 3))then return true; endif;
		iC = apxList.NumEntries();
		var string sName;
		for(i=0)cond(i<iC)iter(i++)do
			if(apxList[i]==null)then continue; endif;
			if(apxList[i]^.GetType() == 2 || apxList[i]^.GetType() == 0)then continue; endif;
			sName = CLocalizer.Get().Translate(apxList[i]^.GetName());
			if(m_pxFilter^.FindItem(sName) != -1)then continue; endif;
			if(sName == CLocalizer.Get().Translate(m_sOwnName))then continue; endif;
			m_pxFilter^.AddItem(sName);
		endfor;
		m_apxPlayerList = apxList;

		return true;
	endproc;
	
	proc bool UpdatePlayerNames(ref string p_rsText)
		var int i,iC=m_asPlayerNames.NumEntries();
		for(i=0)cond(i<iC)iter(i++)do
			if(p_rsText.Find(m_asPlayerNames[i])!=-1)then
				p_rsText.Replace(m_asPlayerNames[i],CLocalizer.Get().Translate(m_asPlayerNames[i]));
				return true;
			endif;
		endfor;
		return false;
	endproc;
	
	proc bool OnTimer(int p_iID)
		if(p_iID==m_iUpdateTimer)then
			UpdatePlayerList();
		elseif(p_iID==m_iTimerCheat)then
			m_iCounter++;
			if(m_iCounter >= m_iCounterMax)then
				m_iCounter=-m_iCounterMax;
			endif;
			var real fIntensity = m_iCounter.ToReal();
			fIntensity /= m_iCounterMax.ToReal();
			fIntensity *= Math.Pi();
			fIntensity = Math.Sin(fIntensity)*255.0;
			var int iNtensity = fIntensity.ToInt();
			if(iNtensity < 0)then
				iNtensity=0;
			endif;
			// color flash
			var color xCol;
			xCol.FromString((192+(iNtensity/4)).ToString()+" "+(iNtensity/2).ToString()+" "+(iNtensity/2).ToString()+" 255");
			m_pxCheat^.SetTextColor(xCol);
			var int iLen = m_sCheat.GetLength();
			m_sCheat = m_sCheat.Right(iLen-1)+m_sCheat.Left(1);
			m_pxCheat^.SetText(m_sCheat.Left(m_iMaxCheatLength));
		endif;
		return true;
	endproc;
	
	export proc void CheatCheck()
		m_pxCheat=cast<CStaticCtrlEx>(GetControl("Warning"));
		var bool bCheatsOn = CGameWrap.GetGame().GetAttribInt("CheatsEnabled")==1;
		if(bCheatsOn&&!m_bCheatWarningInited)then
			m_bCheatWarningInited=true;
			m_iCounterMax = 30;
			var string sAdd="                                       ---------                                       ";
			m_sCheat = CLocalizer.Get().Translate("_UI_ChatWindow_Cheats_Warning")+"    "+CLocalizer.Get().Translate("_UI_MainMenu_Cheats")+sAdd;
			m_iMaxCheatLength = 180;
			m_iTimerCheat = SetTimer(80, true);
			OnTimer(m_iTimerCheat);
		elseif(!m_bCheatWarningInited)then
			m_pxCheat^.SetVisible(false);
		endif;
	endproc;
	
	export proc void AdjustCW(int p_iState)
		var bool bCheatsOn = CGameWrap.GetGame().GetAttribInt("CheatsEnabled")==1;
		if(bCheatsOn&&m_pxCheat!=null)then
			UnsetTimer(m_iTimerCheat);
			m_iCounterMax = 30;
			m_iMaxCheatLength = 180;
			var string sAdd="                                       ---------                                       ";
			if(p_iState==1)then
				m_iMaxCheatLength = 60;
				sAdd="       -----       ";
			endif;
			m_sCheat = CLocalizer.Get().Translate("_UI_ChatWindow_Cheats_Warning")+"    "+CLocalizer.Get().Translate("_UI_MainMenu_Cheats")+sAdd;
			m_iTimerCheat = SetTimer(80, true);
			OnTimer(m_iTimerCheat);
		endif;
	endproc;
	
	export proc void OnWhisper(string p_sFrom, string p_sText, int p_iType)
		var string sPrefix;
		var string sSuffix;
		if(p_iType==0)then
			sPrefix = "!";
			sSuffix = CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_whispers");
		elseif(p_iType==1)then
			sPrefix = "ยง";
			sSuffix = ": ";
		elseif(p_iType==2)then
			sPrefix = "$";
			sSuffix = ": ";
		elseif(p_iType==4)then
			sPrefix="";
			sSuffix=" "+CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_Whispers");
		elseif(p_iType==5)then
			sPrefix="";
			sSuffix=": ";
		endif;
		OnChatMsg(sPrefix+p_sFrom+sSuffix+p_sText);
	endproc;
	
	export proc void WhisperTo(int p_iPlayerID, string p_sTo, string p_sText)
		if(p_sText!="" && p_sText != CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then
			OnChatMsg(CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_Youtold_A") +p_sTo+CLocalizer.Get().Translate("_MIRAGE_UI_ChatWind_Msg_Youtold_B")+p_sText);
			p_sText=" "+">MsgWhisper<"+p_sText;
			CEvt_ChatMsg.Send(p_sText,01h<<p_iPlayerID);
		endif;
	endproc;

	export proc void SpeakToFriends(string p_sText)
		var string sMsg = p_sText;
		var array int aiList;
		CMultiPlayerClientMgr.Get().GetFriends(aiList);
		var int i, iC = aiList.NumEntries();
		sMsg.Replace("&", ":und:");
		if(sMsg!="" && sMsg != CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then
			sMsg=" "+">MsgAllies<"+ sMsg;
			for(i=0)cond(i<iC)iter(i++)do
				CEvt_ChatMsg.Send(sMsg,01h<<aiList[i]);
			endfor;
			OnChatMsg(CLocalizer.Get().Translate("_UI_ChatWind_Msg_ToAllies")+p_sText);
		endif;
	endproc;

	export proc void SpeakToFoes(string p_sText)	
		var string sMsg = p_sText;
		var array int aiList;
		CMultiPlayerClientMgr.Get().GetFoes(aiList);
		sMsg.Replace("&", ":und:");
		var int i, iC = aiList.NumEntries();
		if(sMsg!="" && sMsg != CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then
			sMsg=" "+">MsgEnemies<"+sMsg;
			for(i=0)cond(i<iC)iter(i++)do
				CEvt_ChatMsg.Send(sMsg,01h<<aiList[i]);
			endfor;
			OnChatMsg(CLocalizer.Get().Translate("_UI_ChatWind_Msg_ToEnemies")+p_sText);
		endif;
	endproc;
	
	export proc bool OnSendText()
		var string sMsg = m_pxChatEdit^.GetText();
		m_pxChatEdit^.SetText("");
		if(CChat.IsCommand(sMsg))then
			return true;
		elseif(m_pxFilter^.GetSelectedItem()==1)then
			SpeakToFriends(sMsg);
			return true;
		elseif(m_pxFilter^.GetSelectedItem()==2)then
			SpeakToFoes(sMsg);
			return true;
		elseif(m_pxFilter^.GetSelectedItem()>2)then
			var array ^CPlayerInfoSlot apxList;
			CMultiPlayerClientMgr.Get().GetPlayerList(apxList,true);
			var int i, iC = apxList.NumEntries();
			for(i=0)cond(i<iC)iter(i++)do
				if(apxList[i]==null)then continue; endif;
				if((m_pxFilter^.GetSelectedItemAsString()==CLocalizer.Get().Translate(apxList[i]^.GetName())))then
					WhisperTo(apxList[i]^.GetIndex(),CLocalizer.Get().Translate(apxList[i]^.GetName()), sMsg);
					break;
				endif;
			endfor;
			return true;
		endif;
		if(sMsg!="" && sMsg != CLocalizer.Get().Translate("_ChatWindow_TX_Entertexthere"))then
			sMsg=": "+sMsg;			
			CEvt_ChatMsg.Send(sMsg);
		endif;
		return(true);
	endproc;

	proc bool OnUpdateEdit()
		if(m_pxChatEdit^.GetActive())then
			OnSendText();
		endif;
		return true;
	endproc;
	
	proc void OnChatMsg(string p_sMsg)
		var ^CStaticCtrl pxBackground;
		var string sCDKH;
		var int i, iC = m_apxPlayerList.NumEntries();
		for(i=0)cond(i<iC)iter(i++)do
			if(m_apxPlayerList[i]==null)then continue; endif;
			var ^CLevelInfo.CPlayerSlot pxPlayerSlot=m_apxPlayerList[i]^.GetPlayerSlot();
			if(pxPlayerSlot==null)then continue; endif;
			var string sName = pxPlayerSlot^.GetName();
			if(p_sMsg.Find(sName)!=-1)then
				sCDKH = pxPlayerSlot^.GetValue("key_hash");
				if(CMirageClnMgr.Get().GetCustomMapPlayerSettingBool(CMirageClnMgr.Get().GetMapName(),pxPlayerSlot^.GetHeadQuater(),"Name"))then
					p_sMsg.Replace(sName,CLocalizer.Get().Translate(sName));
					sName = CLocalizer.Get().Translate(sName);
				endif;
				var ^CGame pxGame = ^(CGameWrap.GetGame());
				if(pxGame^.GetAttribInt("RandomColor")==0)then
					var string sPlayer="\\{codc"+pxPlayerSlot^.GetColor().ToString()+"}"+sName+"\\{cogiw_"+m_iLastColor.ToString()+"}";
					p_sMsg.Replace(sName,sPlayer);
				endif;
				break;
			endif;
		endfor;
		//Trying to translate all names that have Localized strings
		UpdatePlayerNames(p_sMsg);
		if(p_sMsg.Find("_UI_PlLiWind_Msg_Gamestartingnow")!=-1)then
			p_sMsg.Replace("_UI_PlLiWind_Msg_Gamestartingnow",CLocalizer.Get().Translate("_UI_PlLiWind_Msg_Gamestartingnow"));
		elseif(p_sMsg.Find("_UI_PlLiWind_Option_Gamestarting")!=-1)then
			p_sMsg.Replace("_UI_PlLiWind_Option_Gamestarting",CLocalizer.Get().Translate("_UI_PlLiWind_Option_Gamestarting"));
		elseif(p_sMsg.Find("_UI_PlLiWind_Msg_Startaborted")!=-1)then
			p_sMsg.Replace("_UI_PlLiWind_Msg_Startaborted",CLocalizer.Get().Translate("_UI_PlLiWind_Msg_Startaborted"));
		endif;
		
		if(m_iLastColor==1)then
			m_iLastColor=2;
		else 
			m_iLastColor=1;
		endif;
		
		var string sTimestamp = "[" + Math2.GetTimeString(CTimeMgr.Get().GetTime().GetSecondsI()) + "] ";
		var string sDev = "";
		if(CChat.CheckDevStatus(sCDKH))then
			sDev = "\\{cocyan}[DEV]\\{cogiw_"+m_iLastColor.ToString()+"}";
		endif;
		
		p_sMsg = "\\{fixed}\\{cogiw_"+m_iLastColor.ToString()+"}" + sTimestamp + sDev + p_sMsg+"\\{/fixed}\\{p}";
		
		m_sChatMessages+=p_sMsg;
		m_pxChatList^.SetText(m_sChatMessages);
		OnResize();
	endproc;
	
	proc bool OnResize()
		super.OnResize();
		m_pxChatList^.OnUpdateContent();
		if(m_bScrollToEnd)then
			m_pxChatList^.ScrollToV(999999999);
		endif;
		return true;
	endproc;
	
	export proc bool ReInit()
		CGameWrap.SetProcRefOnChatMsg(OnChatMsg);
		return(true);
	endproc;
	
	export proc bool OnChangeAutoScroll()
		if(m_pxAutoScroll^.GetChecked()==1)then
			m_bScrollToEnd=true;
			m_pxChatList^.ScrollToV(999999999);
		else
			m_bScrollToEnd=false;
		endif;
		return true;
	endproc;
	
endclass;