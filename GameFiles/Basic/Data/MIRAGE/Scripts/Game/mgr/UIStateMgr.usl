class CUIStateMgr
	
	class CGameInfo
		//Contains all information about the game
		
		export var string m_sIPAddress;
		
		export constructor()
		endconstructor;
	
		destructor()
		enddestructor;
		
	endclass;
	
	class CLoadScreenInfo
		export const int	LOADSCR_INVALID = 0;
		export const int	LOADSCR_MULTIPLAYER = 1;
		export const int	LOADSCR_SINGLEPLAYER = 2;
		export const int	LOADSCR_SAVEGAME = 3;
		export const int	LOADSCR_EMPTY = 4;
		
		export var int		m_iGameType;
		export var string	m_sLevelName;
		export var string	m_sLevelCheckSumme;
		
		export constructor()
			m_iGameType = LOADSCR_INVALID;
			m_sLevelName = "";
			m_sLevelCheckSumme = "";
		endconstructor;
		
		export proc ref CUIStateMgr.CLoadScreenInfo op_Assign(ref CUIStateMgr.CLoadScreenInfo p_rxOther)
			m_iGameType = p_rxOther.m_iGameType;
			m_sLevelName = p_rxOther.m_sLevelName;
			m_sLevelCheckSumme = p_rxOther.m_sLevelCheckSumme;
			return (this)^;
		endproc;
		
		export proc void Invalidate()
			m_iGameType = LOADSCR_INVALID;
			m_sLevelName = "";
			m_sLevelCheckSumme = "";
		endproc;
		
		export proc void Set(int p_iGameType, string p_sLevelName, string p_sLevelCheckSumme)
			var string sMapName = p_sLevelName;
			sMapName.MakeLower();
			sMapName.Replace(" ","_");
			m_iGameType = p_iGameType;
			m_sLevelName = sMapName;
			m_sLevelCheckSumme = p_sLevelCheckSumme;
		endproc;
		
		export proc string ToString()
			return "CLoadScreenInfo(type: "+m_iGameType.ToString()+", name: "+m_sLevelName+", cksum: "+m_sLevelCheckSumme+")";
		endproc;
		
	endclass;
	
	//State definition
	export const int					STATE_INVALID=0;				//Invalid.
	export const int					STATE_LOADING=1;				//Loading is in progress.
	export const int					STATE_MAINMENU=2;				//Main window is active.
	export const int					STATE_INGAME=3;					//Ingame. Game is running.
	export const int					STATE_CREDITS=4;				//Credits window is active.
	export const int					STATE_OPTIONSMENU=5;			//Options window is active.
	export const int					STATE_SINGLEPLAYERMENU=6;		//Singleplayer window is active.
	export const int					STATE_LOADCAMPAIGNLEVEL=7;		//Campaign info window is active.
	export const int					STATE_STARTCAMPAIGN=8;			//Campaign info window is active.
	export const int					STATE_GAMEEND=9;				//Endgame window is active.
	export const int					STATE_GAMEMENU=10;				//Gamemenu window is active.
	export const int					STATE_CREDITSSCREEN=11;			//Credits window.
	//export const int					STATE_QUESTWINDOW=12;			//Quest window.
	//export const int					STATE_DIPLOMACYWINDOW=13;		//Diplomacy window.
	export const int					STATE_LOADGAMEMENU=14;			//Loadgame window.
	export const int					STATE_MULTIPLAYERSELECT=15;		//Multiplayer selectiuon window.
	export const int					STATE_MULTIPLAYERJOINIP=16;		//Join IP screen.
	export const int					STATE_MULTIPLAYERHOSTIP=17;		//Host IP screen.
	export const int					STATE_MULTIPLAYERPREGAME=18;	//Pregame window.
	export const int					STATE_MULTIPLAYERLOBBY=19;		//Lobby window.
	export const int					STATE_POINTBUYSCREEN=20;		//PointBuy window.
	export const int					STATE_HOSTINGDIRECTGAME=21;		//Starting network server.
	export const int					STATE_SAVEGAMEMENU=22;			//Savegame window.
	export const int					STATE_NEWMULTIPLAYERLOBBY=23;	//Lobby window.
	export const int					STATE_NEWMULTIPLAYERPREGAME=24;	//New pregame window.
	export const int					STATE_SKIRMISH=25;				//Skirmish window.	
	export const int					STATE_RELOADGAME=26;			//Campaign info window is active.
	export const int					STATE_JOINTOMULTIPLAYERGAME=27; //Window while connecting to a game
	export const int 					STATE_ACKSERVERLOST=28;			//small window appearing when the server is lost	
	export const int 					STATE_STARTSKIRMISHGAME=29;		//Load Skirmish game
	export const int					STATE_CONTINUECAMPAIGN=30;		//Continue after win game
	export const int					STATE_HELPMENU=31;			//Help menu (in main menu) is active
	export const int					STATE_LURKER=32;			//watching after game has ended
	
	export proc string StateToString(int p_iState)
		if(p_iState==STATE_INVALID) then 			return "STATE_INVALID"; endif;
		if(p_iState==STATE_LOADING) then 			return "STATE_LOADING"; endif;
		if(p_iState==STATE_MAINMENU) then		 	return "STATE_MAINMENU"; endif;
		if(p_iState==STATE_INGAME) then 				return "STATE_INGAME"; endif;
		if(p_iState==STATE_CREDITS) then 			return "STATE_CREDITS"; endif;
		if(p_iState==STATE_OPTIONSMENU) then 		return "STATE_OPTIONSMENU"; endif;
		if(p_iState==STATE_SINGLEPLAYERMENU) then 	return "STATE_SINGLEPLAYERMENU"; endif;
		if(p_iState==STATE_LOADCAMPAIGNLEVEL) then 	return "STATE_LOADCAMPAIGNLEVEL"; endif;
		if(p_iState==STATE_STARTCAMPAIGN) then 		return "STATE_STARTCAMPAIGN"; endif;
		if(p_iState==STATE_GAMEEND) then 			return "STATE_GAMEEND"; endif;
		if(p_iState==STATE_GAMEMENU) then 			return "STATE_GAMEMENU"; endif;
		if(p_iState==STATE_CREDITSSCREEN) then 		return "STATE_CREDITSSCREEN"; endif;
		if(p_iState==STATE_LOADGAMEMENU) then 		return "STATE_LOADGAMEMENU"; endif;
		if(p_iState==STATE_MULTIPLAYERSELECT) then 	return "STATE_MULTIPLAYERSELECT"; endif;
		if(p_iState==STATE_MULTIPLAYERJOINIP) then 	return "STATE_MULTIPLAYERJOINIP"; endif;
		if(p_iState==STATE_MULTIPLAYERHOSTIP) then 	return "STATE_MULTIPLAYERHOSTIP"; endif;
		if(p_iState==STATE_MULTIPLAYERPREGAME) then	return "STATE_MULTIPLAYERPREGAME"; endif;
		if(p_iState==STATE_MULTIPLAYERLOBBY) then 	return "STATE_MULTIPLAYERLOBBY"; endif;
		if(p_iState==STATE_POINTBUYSCREEN) then 		return "STATE_POINTBUYSCREEN"; endif;
		if(p_iState==STATE_HOSTINGDIRECTGAME) then 	return "STATE_HOSTINGDIRECTGAME"; endif;
		if(p_iState==STATE_SAVEGAMEMENU) then 		return "STATE_SAVEGAMEMENU"; endif;
		if(p_iState==STATE_NEWMULTIPLAYERLOBBY) then return "STATE_NEWMULTIPLAYERLOBBY"; endif;
		if(p_iState==STATE_NEWMULTIPLAYERPREGAME) then return "STATE_NEWMULTIPLAYERPREGAME"; endif;
		if(p_iState==STATE_SKIRMISH) then 			return "STATE_SKIRMISH"; endif;
		if(p_iState==STATE_RELOADGAME) then			return "STATE_RELOADGAME"; endif;
		if(p_iState==STATE_JOINTOMULTIPLAYERGAME) then return "STATE_JOINTOMULTIPLAYERGAME"; endif;
		if(p_iState==STATE_ACKSERVERLOST) then 		return "STATE_ACKSERVERLOST"; endif;
		if(p_iState==STATE_STARTSKIRMISHGAME) then 	return "STATE_STARTSKIRMISHGAME"; endif;
		if(p_iState==STATE_CONTINUECAMPAIGN) then 	return "STATE_CONTINUECAMPAIGN"; endif;			
		if(p_iState==STATE_HELPMENU) then 		return "STATE_HELPMENU"; endif;
		if(p_iState==STATE_LURKER) then 		return "STATE_LURKER"; endif;
		return "invalid";
	endproc;
	
	//...add your state here if necessary.
	
	var CLoadScreenInfo				m_xLoadScreenInfo;
	
	var int							m_iState;
	
	var ^CDesktop 					m_pxDesktop;
	
	var ^CMenuOverlay				m_pxMenuOverlay;
	
	//Screens
	var ^CMainMenu						m_pxMainMenu;
	var ^CCampaignMenu					m_pxCampaignMenu;
	var ^COptionsMenu					m_pxOptionsMenu;
	var ^CEndScreen						m_pxEndScreen;
	var ^CLoadScreen					m_pxLoadScreen;
	var ^CInGameScreen					m_pxInGameScreen;
	var ^CGameMenu						m_pxGameMenu;		
	var ^CLoadSaveGameMenu2				m_pxLoadSaveGameMenu;	
	var ^CMultiPlayerSelectScreen		m_pxMultiPlayerSelectScreen;
	var ^CMultiPlayerJoinIPScreen		m_pxMultiPlayerJoinIPScreen;
	var ^CMultiPlayerHostIPScreen		m_pxMultiPlayerHostIPScreen;
	var ^CNewMultiPlayerPreGameWindow	m_pxNewMultiPlayerPreGameWindow;
	var ^CNewMultiPlayerPreGameWindow	m_pxSkirmishWindow;
	var ^CNewMultiPlayerLobby			m_pxNewMultiPlayerLobbyWindow;	
	var ^CJoinToMultiPlayerGameWindow 	m_pxJoinToMultiPlayerGameWindow;
	var ^CAckServerLostWindow		 	m_pxAcknowledgeServerLostWindow;
	var ^CStaticCtrlEx				 	m_pxToolTip;
	var ^CHelpMenu					m_pxHelpMenu;
	var array ^CStateUIWnd				m_apxMenus;
	static var ^CUIStateMgr				ms_pxInst;
	var	^CStaticCtrl			m_pxTmp;
	
	static var array int				ms_aiColors;
	static var array int				ms_aiCounterColors;
	var int							m_iMenuOffsetX,m_iMenuOffsetY;
	
	var string m_sCurBGVideo;
	
	var string m_sSelectedMapFile;
	
	
	export constructor()
		m_pxDesktop=CClientWrap.GetDesktop();
		m_iMenuOffsetX=0;
		m_iMenuOffsetY=0;
		CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
		CWindowMgr.Get().SetToolTipDelay(0);
		m_sSelectedMapFile="";
		
		InitHPColors();
	endconstructor;

	destructor()
		if(m_pxToolTip!=null)then
			m_pxToolTip^.Destroy();
			m_pxToolTip=null;
		endif;
	enddestructor;
	
	export proc void SetSelectedMapFile(string p_sFilePath)
		m_sSelectedMapFile=p_sFilePath;
	endproc;
	
	export proc string GetSelectedMapFile()
		return m_sSelectedMapFile;
	endproc;	
	
	static proc void InitHPColors()
		var int iNumColorsInFile = 12;
		if(ms_aiColors.NumEntries()<=0 || ms_aiCounterColors.NumEntries()<=0)then
			ms_aiColors = iNumColorsInFile*3;
			ms_aiCounterColors = iNumColorsInFile*3;
			
			var CPropDB xColorDB;
			xColorDB.Load(CClientWrap.GetUrsRelPath()+"/data/base/scripts/game/misc/ACColors.txt");
			var int i,iC=iNumColorsInFile;
			for(i=0)cond(i<iC)iter(i++)do
				var int iR=i*3,iG=(i*3)+1,iB=(i*3)+2;
				var ^CPropDB.CNode pxLightNode = xColorDB.FindNode("color"+i.ToString()+"/light",false);
				var ^CPropDB.CNode pxDarkNode = xColorDB.FindNode("color"+i.ToString()+"/dark",false);
				var ^CPropDB.CNode pxCounterNode = xColorDB.FindNode("color"+i.ToString()+"/counter",false);
				if(pxDarkNode!=null)then
					var string sTemp = pxDarkNode^.Value();
					var array string asTemp;
					sTemp.Split(asTemp," ",false);
					if(asTemp.NumEntries()==3)then
						ms_aiColors[iR]=asTemp[0].ToInt();
						ms_aiColors[iG]=asTemp[1].ToInt();
						ms_aiColors[iB]=asTemp[2].ToInt();
					endif;
				endif;
				if(pxCounterNode!=null)then
					var string sTemp = pxCounterNode^.Value();
					var array string asTemp;
					sTemp.Split(asTemp," ",false);
					if(asTemp.NumEntries()==3)then
						ms_aiCounterColors[iR]=asTemp[0].ToInt();
						ms_aiCounterColors[iG]=asTemp[1].ToInt();
						ms_aiCounterColors[iB]=asTemp[2].ToInt();
					endif;
				endif;
			endfor;
		endif;
	endproc;
	
	export proc int GetHPColorValue(int p_iNumber)
		InitHPColors();
		if(p_iNumber>=0 && p_iNumber<ms_aiColors.NumEntries())then
			return ms_aiColors[p_iNumber];
		endif;
		return 0;
	endproc;

	export proc int GetHPCounterColorValue(int p_iNumber)
		InitHPColors();
		if(p_iNumber>=0 && p_iNumber<ms_aiCounterColors.NumEntries())then
			return ms_aiCounterColors[p_iNumber];
		endif;
		return 0;
	endproc;

	export static proc ref CUIStateMgr Get()
		if(ms_pxInst==null)then
			ms_pxInst = new CUIStateMgr;
		endif;
		return(ms_pxInst^);
	endproc;

	export static proc void Kill()
		delete ms_pxInst;
		ms_pxInst = null;
	endproc;
	
	export proc bool Init()
		var int iScreenW=m_pxDesktop^.GetSize().GetX();
		var int iScreenH=m_pxDesktop^.GetSize().GetY();
		m_iMenuOffsetX=(iScreenW - 1024 * CGameInst.ms_iUIScaleFactor)/2;
		m_iMenuOffsetX=Math.Max(m_iMenuOffsetX,0);
		m_iMenuOffsetY=(iScreenH - 20 * CGameInst.ms_iUIScaleFactor - 768 * CGameInst.ms_iUIScaleFactor)/2;
		m_iMenuOffsetY=Math.Max(m_iMenuOffsetY,0);
//		m_iMenuOffsetY=0;
	//	m_iMenuOffsetX=0;
		AddBackground();
		m_pxDesktop^.AddAccelerator("UP, NONE,Up");
		m_pxDesktop^.AddAccelerator("DOWN, NONE,Down");
		m_pxToolTip=new CStaticCtrlEx;
		m_pxToolTip^.SetVisible(false);
		m_pxToolTip^.SetSize(800 * CGameInst.ms_iUIScaleFactor,25 * CGameInst.ms_iUIScaleFactor);
		m_pxToolTip^.SetPos(10 * CGameInst.ms_iUIScaleFactor,600 * CGameInst.ms_iUIScaleFactor);
		m_pxToolTip^.SetFrame(true);
//		m_pxToolTip^.SetFrameType("BtnUp");
//		m_pxToolTip^.SetFrameType("Custom3");
		m_pxToolTip^.SetFrameType("ToolTip");
		m_pxToolTip^.SetTextAlign(CStaticCtrlEx.TAH_Left.ToInt(),CStaticCtrlEx.TAV_Center.ToInt());
		m_pxDesktop^.AddChild(m_pxToolTip);
		return(true);
	endproc;
	
	export proc void SetLoadScreenInfo(int p_iGameType, string p_sLevelName, string p_sLevelCheckSumme)
		m_xLoadScreenInfo.Set(p_iGameType, p_sLevelName, p_sLevelCheckSumme);
	endproc;
	
	export proc void SetLoadScreenInfo(CUIStateMgr.CLoadScreenInfo p_xInfo)
		m_xLoadScreenInfo = p_xInfo;
	endproc;

	export proc ref CUIStateMgr.CLoadScreenInfo GetLoadScreenInfo()
		return m_xLoadScreenInfo;
	endproc;
	
	export proc void InvalidateLoadScreenInfo()
		m_xLoadScreenInfo.Invalidate();
	endproc;
	
	export proc int GetState()
		return(m_iState);
	endproc;
	
	export proc bool SetState(int p_iNewState)
		//KLog.LogWarn("Manni","SetState(): "+p_iNewState.ToString());
		UpdateState(p_iNewState,"");
		CGameStateReporterGame.CheckState();
		return(true);
	endproc;
	
	export proc bool SetState(int p_iNewState, string p_sParam)
		//KLog.LogWarn("Manni","SetState(): "+p_iNewState.ToString()+" , "+p_sParam);
		UpdateState(p_iNewState,p_sParam);
		CGameStateReporterGame.CheckState();
		return(true);
	endproc;

	export proc void HideMenuToolTip()
		m_pxToolTip^.SetVisible(false);
	endproc;

	export proc void ShowMenuToolTip(string p_sText)
		var int iWidth=m_pxDesktop^.GetWidth();
		var int iHeight=m_pxDesktop^.GetHeight();
		m_pxToolTip^.SetText("  "+p_sText);
		m_pxToolTip^.SetVisible(true);
		m_pxToolTip^.SetSize(iWidth,25* CGameInst.ms_iUIScaleFactor);
		m_pxToolTip^.SetPos(0,iHeight-25* CGameInst.ms_iUIScaleFactor);
	endproc;
	
	export proc bool IsIngame()
		return m_iState==STATE_INGAME;
	endproc;
	
	export proc void LooseLoadScreen(^CLoadScreen p_pxLS)
		if(m_pxLoadScreen==p_pxLS)then
			m_pxLoadScreen=null;
		endif;
	endproc;
	
	proc bool UpdateState(int p_iNewState, string p_sParam)
		//KLog.LogSpam("AnTr","UpdatesState New: "+StateToString(p_iNewState)+", Cur: "+StateToString(m_iState));
		if(p_iNewState==STATE_GAMEEND)then
			if(m_pxEndScreen!=null)then
				return true;
			endif;
			if(m_iState==STATE_GAMEMENU)then
				// KLog.LogError("UIStateMgr","Correct Way");
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			elseif(m_iState==STATE_SAVEGAMEMENU || m_iState==STATE_LOADGAMEMENU)then
				RemoveMenu(m_pxLoadSaveGameMenu); m_pxLoadSaveGameMenu=null;
			elseif(m_iState==STATE_OPTIONSMENU)then
				RemoveMenu(m_pxOptionsMenu);m_pxOptionsMenu=null;
			elseif(m_iState==STATE_HELPMENU)then
				RemoveMenu(m_pxHelpMenu);m_pxHelpMenu=null;
			endif;
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.SetVisible(false);
			endif;
			if(m_pxEndScreen!=null)then
				RemoveMenu(m_pxEndScreen);
				m_pxEndScreen=null;
			endif;
			m_pxEndScreen=new CEndScreen(p_sParam);
			AddMenu(m_pxEndScreen);
			//m_pxEndScreen^.Init(p_sParam);
		/*******Main Menu*******/
		elseif(p_iNewState==STATE_MAINMENU)then
			CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
			CWindowMgr.Get().SetToolTipDelay(0);
			ShowMenuToolTip("");
			CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
			CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
			CMultiPlayerClientMgr.Get().SetDirectIPMode(false);
			RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			if(m_pxInGameScreen!=null) then
				m_pxInGameScreen^.SetVisible(false);
				m_pxInGameScreen^.SetDestructionTimer();
				m_pxInGameScreen=null;
				//m_pxInGameScreen^.GetDlgSceneMgr()^.Reset();
			endif;
			if(m_iState==STATE_INVALID)then
				//
			elseif(m_iState==STATE_GAMEMENU)then
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
				if(m_pxInGameScreen!=null)then
					m_pxInGameScreen^.SetVisible(false);
					m_pxInGameScreen^.SetDestructionTimer();
					m_pxInGameScreen=null;
				endif;
				AddBackground();
				CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
				CWindowMgr.Get().SetToolTipDelay(0);
			elseif(m_iState==STATE_GAMEEND)then
				RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
				if(m_pxInGameScreen!=null)then
					m_pxInGameScreen^.SetVisible(false);
					m_pxInGameScreen^.SetDestructionTimer();
					m_pxInGameScreen=null;
				endif;
				AddBackground();
				CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
				CWindowMgr.Get().SetToolTipDelay(0);
			elseif(m_iState==STATE_OPTIONSMENU)then
				RemoveMenu(m_pxOptionsMenu);m_pxOptionsMenu=null;
			elseif(m_iState==STATE_HELPMENU)then
				RemoveMenu(m_pxHelpMenu);m_pxHelpMenu=null;
			elseif(m_iState==STATE_CREDITSSCREEN)then
				//RemoveMenu(m_pxCreditsScreen);m_pxCreditsScreen=null;
			elseif(m_iState==STATE_STARTCAMPAIGN || m_iState==STATE_LOADCAMPAIGNLEVEL || m_iState==STATE_RELOADGAME)then
				RemoveMenu(m_pxCampaignMenu);m_pxCampaignMenu=null;
				AddBackground(RANDOMBACKGROUND);
			elseif(m_iState==STATE_LOADGAMEMENU)then
				RemoveMenu(m_pxLoadSaveGameMenu);	m_pxLoadSaveGameMenu=null;
			elseif(m_iState==STATE_MULTIPLAYERSELECT)then
				RemoveMenu(m_pxMultiPlayerSelectScreen);m_pxMultiPlayerSelectScreen=null;
			elseif(m_iState==STATE_SKIRMISH || m_iState==STATE_STARTSKIRMISHGAME)then
				RemoveMenu(m_pxSkirmishWindow);m_pxSkirmishWindow=null;
			elseif(m_iState==STATE_MULTIPLAYERJOINIP)then
				RemoveMenu(m_pxMultiPlayerJoinIPScreen);m_pxMultiPlayerJoinIPScreen=null;
			elseif(m_iState==STATE_MULTIPLAYERHOSTIP)then
				RemoveMenu(m_pxMultiPlayerHostIPScreen);m_pxMultiPlayerHostIPScreen=null;
			elseif(m_iState==STATE_NEWMULTIPLAYERLOBBY)then
				RemoveMenu(m_pxNewMultiPlayerLobbyWindow);m_pxNewMultiPlayerLobbyWindow=null;
			elseif(m_iState==STATE_ACKSERVERLOST)then
				RemoveMenu(m_pxAcknowledgeServerLostWindow);m_pxAcknowledgeServerLostWindow=null;
				if(m_pxLoadScreen!=null) then
					m_pxLoadScreen^.Destroy();
				endif;
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
				if(m_pxInGameScreen!=null)then
					m_pxInGameScreen^.SetVisible(false);
					m_pxInGameScreen^.SetDestructionTimer();
					m_pxInGameScreen=null;
				endif;
				AddBackground();
				CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
				CWindowMgr.Get().SetToolTipDelay(0);
			endif;
			m_pxMainMenu=new CMainMenu();
			AddMenu(m_pxMainMenu);
			m_pxMainMenu^.Init();
		/*******Options Menu*******/
		elseif(p_iNewState==STATE_OPTIONSMENU)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			elseif(m_iState==STATE_GAMEMENU)then
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			elseif(m_iState==STATE_GAMEEND)then
				RemoveMenu(m_pxEndScreen);
				m_pxEndScreen=null;
			endif;
			m_pxOptionsMenu=new COptionsMenu();
			AddMenu(m_pxOptionsMenu);
			m_pxOptionsMenu^.Init(m_iState);
			if(m_iState==STATE_GAMEEND)then
				m_pxOptionsMenu^.SetESReturnState(p_sParam);
			endif;
		/*******Help Menu*******/
		elseif(p_iNewState==STATE_HELPMENU)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			endif;
			m_pxHelpMenu=new CHelpMenu();
			AddMenu(m_pxHelpMenu);
			//m_pxHelpMenu^.Init(m_iState);
		/*******Start Campaign*******/
		elseif(p_iNewState==STATE_STARTCAMPAIGN)then
			RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			//AddBackground(CAMPAIGNBACKGROUND); disabled for new .tga overlay images
			m_pxCampaignMenu=new CCampaignMenu();
			AddMenu(m_pxCampaignMenu);
			CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
			CWindowMgr.Get().SetToolTipDelay(0);
		elseif(p_iNewState==STATE_CONTINUECAMPAIGN)then
			AddBackground();
			CWindowMgr.Get().m_xOnToolTip=OnShowToolTip;
			CWindowMgr.Get().SetToolTipDelay(0);
			CSoundMgrCln.SetGameIsRunning(false);
			CEvt_ClientDisconnect.Send();
			ShowLoadingScreen("ShuttingServer",STATE_STARTCAMPAIGN);
		elseif(p_iNewState==STATE_LOADCAMPAIGNLEVEL)then
			RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			RemoveMenu(m_pxCampaignMenu);m_pxCampaignMenu=null;
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.SetVisible(false);
				m_pxInGameScreen^.SetGameActive(false);
			endif;
			CCampaignMgr.Get().LoadCurrentLevel();
			CUIStateMgr.Get().ShowLoadingScreen("StartingGame",CUIStateMgr.STATE_INGAME);
			//111
		elseif(p_iNewState==STATE_STARTSKIRMISHGAME)then
			//RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			//RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			//RemoveMenu(m_pxSkirmishWindow);m_pxSkirmishWindow=null;
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.SetVisible(false);
				m_pxInGameScreen^.SetGameActive(false);
			endif;
			CMultiPlayerClientMgr.Get().LoadSkirmishGame();
			CUIStateMgr.Get().InvalidateLoadScreenInfo();
			CUIStateMgr.Get().ShowLoadingScreen("StartingGame",CUIStateMgr.STATE_INGAME);
		elseif(p_iNewState==STATE_RELOADGAME)then
			RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			if(m_pxMenuOverlay!=null)then
				m_pxMenuOverlay^.Destroy();
				m_pxMenuOverlay=null;
			endif;
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.SetVisible(false);
				m_pxInGameScreen^.SetGameActive(false);
			endif;
			CEvt_RestartGame.Send();
			CUIStateMgr.Get().ShowLoadingScreen("StartingGame",CUIStateMgr.STATE_INGAME);
		elseif(p_iNewState==STATE_GAMEMENU)then
			if(m_iState==STATE_INGAME)then
			elseif(m_iState==STATE_GAMEEND)then
				return false;
			elseif(m_iState==STATE_LURKER)then
				return false;
			elseif(m_iState==STATE_OPTIONSMENU)then
				RemoveMenu(m_pxOptionsMenu);m_pxOptionsMenu=null;
				m_pxGameMenu=new CGameMenu();
				AddMenu(m_pxGameMenu);
				m_pxGameMenu^.Init();
				m_iState=p_iNewState;
				return(false);
			elseif(m_iState==STATE_SAVEGAMEMENU)then
				RemoveMenu(m_pxLoadSaveGameMenu);m_pxLoadSaveGameMenu=null;
				m_pxGameMenu=new CGameMenu();
				AddMenu(m_pxGameMenu);
				m_pxGameMenu^.Init();
				m_iState=p_iNewState;
				return(true);
			elseif(m_iState==STATE_LOADGAMEMENU)then
				RemoveMenu(m_pxLoadSaveGameMenu);m_pxLoadSaveGameMenu=null;	
				m_pxGameMenu=new CGameMenu();
				AddMenu(m_pxGameMenu);
				m_pxGameMenu^.Init();
				m_iState=p_iNewState;
				return(true);
			elseif(m_iState==STATE_HELPMENU)then
				RemoveMenu(m_pxHelpMenu);m_pxHelpMenu=null;
				m_pxGameMenu=new CGameMenu();
				AddMenu(m_pxGameMenu);
				m_pxGameMenu^.Init();
				m_iState=p_iNewState;
				return(false);
			endif;
			System.Assert(m_pxInGameScreen!=null,"m_pxInGameScreen!=null");
			m_pxInGameScreen^.SetVisible(false);
			if(m_pxMenuOverlay!=null) then
				m_pxMenuOverlay^.Destroy();
				m_pxMenuOverlay=null;
			endif;
			m_pxMenuOverlay=new CMenuOverlay(false);
			m_pxDesktop^.AddChild(m_pxMenuOverlay);
			m_pxGameMenu=new CGameMenu();
			AddMenu(m_pxGameMenu);
			m_pxGameMenu^.Init();
		/*******Ingame*******/
		elseif(p_iNewState==STATE_INGAME)then
			if(m_iState==STATE_LURKER)then
				return false;
			endif;
			HideMenuToolTip();
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.EnableToolTipWindow();
			endif;
			CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
			CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();

			//L KLog.LogWarn("Manni","Entering InGameState from: "+m_iState.ToString());
			if(m_iState==STATE_STARTCAMPAIGN || m_iState==STATE_LOADCAMPAIGNLEVEL || m_iState==STATE_RELOADGAME)then
				//RemoveMenu(m_pxCampaignInfoScreen);m_pxCampaignInfoScreen=null;
				RemBackground();
			elseif(m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;
				RemBackground();
			elseif(m_iState==STATE_SKIRMISH || m_iState==STATE_STARTSKIRMISHGAME)then
				RemoveMenu(m_pxSkirmishWindow);m_pxSkirmishWindow=null;
				RemBackground();
			elseif(m_iState==STATE_SAVEGAMEMENU)then
				RemoveMenu(m_pxLoadSaveGameMenu);m_pxLoadSaveGameMenu=null;
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
			elseif(m_iState==STATE_GAMEMENU)then
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			elseif(m_iState==STATE_GAMEEND)then
				if(m_pxInGameScreen!=null)then
					m_pxInGameScreen^.SetVisible(true);
				endif;
				RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			elseif(m_iState==STATE_LOADGAMEMENU)then
				if(m_pxLoadSaveGameMenu^.GetReturnState()==STATE_MAINMENU)then
					RemBackground();
				endif;
				RemoveMenu(m_pxLoadSaveGameMenu);m_pxLoadSaveGameMenu=null;
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
			elseif(m_iState==STATE_MAINMENU)then
				RemBackground();
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
				if(m_pxMenuOverlay!=null)then
					m_pxMenuOverlay^.Destroy();
					m_pxMenuOverlay=null;
				endif;
			endif;
			if(m_pxInGameScreen==null)then
				m_pxInGameScreen=new CInGameScreen(m_pxDesktop);
				m_pxInGameScreen^.SetVisible(false);
				m_pxDesktop^.AddChild(m_pxInGameScreen);
			endif;
			if(!m_pxInGameScreen^.GetGameActive())then
				CUIStateMgr.Get().ShowLoadingScreen("StartingGame",STATE_INGAME);
			else
				CWindowMgr.Get().BringWindowToTop(m_pxInGameScreen,true);
				m_pxInGameScreen^.SetVisible(true);
				CIdleAnimMgr.Get();
			endif;
		/*******Credits Screen*******/
		elseif(p_iNewState==STATE_CREDITSSCREEN)then
			p_iNewState=STATE_MAINMENU;
			CClientWrap.PlayBink("credits.bik");
		/*******LoadGame Window*******/
		elseif(p_iNewState==STATE_LOADGAMEMENU)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			elseif(m_iState==STATE_GAMEMENU)then
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			elseif(m_iState==STATE_GAMEEND)then
				p_sParam=m_pxEndScreen^.GetPath();
				RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			endif;
			m_pxLoadSaveGameMenu=new CLoadSaveGameMenu2();
			AddMenu(m_pxLoadSaveGameMenu);
			if(m_iState==STATE_GAMEEND)then
				m_pxLoadSaveGameMenu^.Init(true,false,p_sParam);
			else
				m_pxLoadSaveGameMenu^.Init(true);
			endif;
		elseif(p_iNewState==STATE_SAVEGAMEMENU)then
			if(m_iState==STATE_GAMEMENU)then
				RemoveMenu(m_pxGameMenu);m_pxGameMenu=null;
			elseif(m_iState==STATE_GAMEEND)then
				RemoveMenu(m_pxEndScreen);m_pxEndScreen=null;
			endif;
			m_pxLoadSaveGameMenu=new CLoadSaveGameMenu2();
			AddMenu(m_pxLoadSaveGameMenu);
			if(m_iState==STATE_GAMEEND)then
				m_pxLoadSaveGameMenu^.Init(false,true,p_sParam);
			else
				m_pxLoadSaveGameMenu^.Init(false,false);
			endif;
		/*******Multiplayer Select Window*******/
		elseif(p_iNewState==STATE_MULTIPLAYERSELECT)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			elseif(m_iState==STATE_MULTIPLAYERJOINIP)then
				RemoveMenu(m_pxMultiPlayerJoinIPScreen);m_pxMultiPlayerJoinIPScreen=null;
			elseif(m_iState==STATE_MULTIPLAYERHOSTIP)then
				RemoveMenu(m_pxMultiPlayerHostIPScreen);m_pxMultiPlayerHostIPScreen=null;
			elseif(m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;	
			elseif(m_iState==STATE_NEWMULTIPLAYERLOBBY)then
				RemoveMenu(m_pxNewMultiPlayerLobbyWindow);m_pxNewMultiPlayerLobbyWindow=null;
				AddBackground();
			endif;	
			m_pxMultiPlayerSelectScreen=new CMultiPlayerSelectScreen();
			AddMenu(m_pxMultiPlayerSelectScreen);
			m_pxMultiPlayerSelectScreen^.Init(m_iState);
		elseif(p_iNewState==STATE_MULTIPLAYERJOINIP)then
			if(m_iState==STATE_MULTIPLAYERSELECT)then
				RemoveMenu(m_pxMultiPlayerSelectScreen);m_pxMultiPlayerSelectScreen=null;			
			elseif(m_iState==STATE_JOINTOMULTIPLAYERGAME)then
				RemoveMenu(m_pxJoinToMultiPlayerGameWindow);m_pxJoinToMultiPlayerGameWindow=null;
			elseif(m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
				CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;
			elseif(m_iState==STATE_ACKSERVERLOST)then
				RemoveMenu(m_pxAcknowledgeServerLostWindow); m_pxAcknowledgeServerLostWindow=null;
			endif;
			m_pxMultiPlayerJoinIPScreen=new CMultiPlayerJoinIPScreen();
			AddMenu(m_pxMultiPlayerJoinIPScreen);
			m_pxMultiPlayerJoinIPScreen^.Init(m_iState);
		elseif(p_iNewState==STATE_MULTIPLAYERHOSTIP)then
			if(m_iState==STATE_MULTIPLAYERSELECT)then
				RemoveMenu(m_pxMultiPlayerSelectScreen);m_pxMultiPlayerSelectScreen=null;
			/*elseif(m_iState==STATE_MULTIPLAYERHOSTIP)then
				RemoveMenu(m_pxMultiPlayerHostIPScreen);m_pxMultiPlayerHostIPScreen=null;
			elseif (m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
				CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;*/
			endif;
			m_pxMultiPlayerHostIPScreen=new CMultiPlayerHostIPScreen();
			AddMenu(m_pxMultiPlayerHostIPScreen);
			m_pxMultiPlayerHostIPScreen^.Init(m_iState);
		elseif(p_iNewState==STATE_SKIRMISH)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			endif;
			var string sName;
			var ^CUserProfileList pxList=^(CClientWrap.GetUserProfileList());
			if(pxList!=null)then
				sName=pxList^.GetCurrentProfile()^.GetUserName();
				if(sName=="")then
					sName=Windows.GetUserName();
					CSettings.Set("Game/PlayerName",sName);
				endif;
			endif;
			var string sMap;
			if(!CSettings.Get("Game/RecentSettingsSkirmish/MapFileName",sMap))then
				var Filesystem.CFileList xFileList;
				var string sPath=CClientWrap.GetUrsRelPath()+"/Data/Base/Maps/Multiplayer/";
				xFileList.ReadListEx(sPath,"*.ula",false,false);
				if(xFileList.NumEntries()>0)then
					sMap=xFileList[0].m_sName;
				else
					return(false);
				endif;
				CSettings.Set("Game/RecentSettingsSkirmish/MapFileName",sMap);
			endif;
			sName.Replace("&",":und:");
			var string sServer=Network.GetLocalIPAddress().ToString();
			CEvt_GenericEvent.Send("host_multiplayer&"+CGameWrap.GetClientID().ToString()+"&"+sMap+"&"+sName+"&"+"2"+"&"+"1000"+"&"+sServer);
			CMultiPlayerClientMgr.Get().SetSkirmishMode(true);
			CMultiPlayerClientMgr.Get().SetMap(sMap);
			m_pxSkirmishWindow=new CNewMultiPlayerPreGameWindow();
			AddMenu(m_pxSkirmishWindow);
		elseif(p_iNewState==STATE_NEWMULTIPLAYERPREGAME)then
			if(m_iState==STATE_MULTIPLAYERHOSTIP)then
				RemoveMenu(m_pxMultiPlayerHostIPScreen);m_pxMultiPlayerHostIPScreen=null;
			elseif(m_iState==STATE_MULTIPLAYERJOINIP)then
				RemoveMenu(m_pxMultiPlayerJoinIPScreen);m_pxMultiPlayerJoinIPScreen=null;
			elseif(m_iState==STATE_NEWMULTIPLAYERLOBBY)then
				RemoveMenu(m_pxNewMultiPlayerLobbyWindow);m_pxNewMultiPlayerLobbyWindow=null;
			elseif(m_iState==STATE_JOINTOMULTIPLAYERGAME)then
				RemoveMenu(m_pxJoinToMultiPlayerGameWindow);m_pxJoinToMultiPlayerGameWindow=null;
			endif;
			m_pxNewMultiPlayerPreGameWindow=new CNewMultiPlayerPreGameWindow();
			AddMenu(m_pxNewMultiPlayerPreGameWindow);
		elseif(p_iNewState==STATE_NEWMULTIPLAYERLOBBY)then
			if(m_iState==STATE_MAINMENU)then
				RemoveMenu(m_pxMainMenu);m_pxMainMenu=null;
			endif;
			if(m_iState==STATE_MULTIPLAYERSELECT)then
				RemoveMenu(m_pxMultiPlayerSelectScreen);m_pxMultiPlayerSelectScreen=null;
			endif;
			if(m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
				CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;
			endif;
			if(m_iState==STATE_JOINTOMULTIPLAYERGAME)then
				RemoveMenu(m_pxJoinToMultiPlayerGameWindow);m_pxJoinToMultiPlayerGameWindow=null;
			endif;
			if(m_iState==STATE_ACKSERVERLOST)then
				RemoveMenu(m_pxAcknowledgeServerLostWindow); m_pxAcknowledgeServerLostWindow=null;
			endif;
			m_pxNewMultiPlayerLobbyWindow=new CNewMultiPlayerLobby(p_sParam);
			AddMenu(m_pxNewMultiPlayerLobbyWindow);
			m_pxNewMultiPlayerLobbyWindow^.Init(m_iState);
		elseif(p_iNewState==STATE_JOINTOMULTIPLAYERGAME)then
			if(m_iState==STATE_NEWMULTIPLAYERLOBBY)then
				RemoveMenu(m_pxNewMultiPlayerLobbyWindow);m_pxNewMultiPlayerLobbyWindow=null;
			endif;
			if(m_iState==STATE_MULTIPLAYERJOINIP)then
				RemoveMenu(m_pxMultiPlayerJoinIPScreen);m_pxMultiPlayerJoinIPScreen=null;
			endif;
			m_pxJoinToMultiPlayerGameWindow=new CJoinToMultiPlayerGameWindow;
			AddMenu(m_pxJoinToMultiPlayerGameWindow);
			m_pxJoinToMultiPlayerGameWindow^.Init(m_iState);
		elseif(p_iNewState==STATE_ACKSERVERLOST)then
			if(m_iState==STATE_NEWMULTIPLAYERPREGAME)then
				CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
				CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
				RemoveMenu(m_pxNewMultiPlayerPreGameWindow);m_pxNewMultiPlayerPreGameWindow=null;
			elseif(m_iState==STATE_NEWMULTIPLAYERLOBBY) then
				RemoveMenu(m_pxNewMultiPlayerLobbyWindow);m_pxNewMultiPlayerLobbyWindow=null;
			endif;
			if(m_iState==STATE_INGAME)then
			endif;
			if(CUIMgr.MakroRunning())then
				CEvt_CloseGame.Send();
				SetState(STATE_MAINMENU);
				return true;
			endif;
			m_pxAcknowledgeServerLostWindow=new CAckServerLostWindow;
			AddMenu(m_pxAcknowledgeServerLostWindow);
			if(p_sParam.IsEmpty()) then
				m_pxAcknowledgeServerLostWindow^.Init(m_iState);
			else
				m_pxAcknowledgeServerLostWindow^.Init(m_iState,p_sParam);
			endif;
		/*******Lurker*******/
		elseif(p_iNewState==STATE_LURKER)then
			HideMenuToolTip();
			CMultiPlayerClientMgr.Get().m_xOnLevelInfoUpdate.Clear();
			CMultiPlayerClientMgr.Get().m_xOnPointBuyUpdate.Clear();
			if(m_iState==STATE_GAMEEND)then
				if(m_pxInGameScreen!=null)then
					m_pxInGameScreen^.SetVisible(true);
				endif;
				RemoveMenu(m_pxEndScreen);
				m_pxEndScreen=null;
			endif;
			if(m_pxInGameScreen^.GetGameActive())then
				m_pxInGameScreen^.SetESReturnState(p_sParam);
				CWindowMgr.Get().BringWindowToTop(m_pxInGameScreen,true);
				m_pxInGameScreen^.SetVisible(true);
				CIdleAnimMgr.Get();
			endif;
		endif;
		m_iState=p_iNewState;
		return(true);
	endproc;
	
	export proc bool LeaveGame()
		m_pxInGameScreen^.SetVisible(false);
		m_pxInGameScreen^.SetDestructionTimer();
		CEvt_ClientDisconnect.Send();
		ShowLoadingScreen("ShuttingServer",STATE_LOADCAMPAIGNLEVEL);
		SetState(STATE_LOADCAMPAIGNLEVEL);
		return(true);
	endproc;

	export proc void SetLoadingProgress(real p_fProgress, string p_sDescription)
		if(m_pxLoadScreen==null)then return; endif;
		m_pxLoadScreen^.SetProgress(p_fProgress, p_sDescription);
	endproc;
	
	
	export proc int GetLoadWindowState()
		if(m_pxLoadSaveGameMenu!=null)then
			return(m_pxLoadSaveGameMenu^.GetReturnState());
		else
			return(0);	
		endif;
	endproc;
	
	export proc bool ShowLoadingScreen(string p_sTask,int p_iReturnState)
		if(m_pxLoadScreen == null)then
			m_pxLoadScreen=new CLoadScreen();
			m_pxDesktop^.AddChild(m_pxLoadScreen);
		endif;
		CWindowMgr.Get().BringWindowToTop(m_pxLoadScreen,true);
		CWindowMgr.Get().SetModal(m_pxLoadScreen);
		m_pxLoadScreen^.Init(p_sTask,p_iReturnState);
		return(true);
	endproc;

	export proc bool ShowLoadingScreen(string p_sTask, int p_iReturnState, procref<bool> p_xCallBack)
		if(m_pxLoadScreen == null)then
			m_pxLoadScreen=new CLoadScreen();
			m_pxDesktop^.AddChild(m_pxLoadScreen);
		endif;
		CWindowMgr.Get().BringWindowToTop(m_pxLoadScreen,true);
		CWindowMgr.Get().SetModal(m_pxLoadScreen);
		m_pxLoadScreen^.Init(p_sTask,p_iReturnState,p_xCallBack);
		return(true);
	endproc;

	/*
	export proc bool StartNextCampaignLevel()
		if(m_pxCampaignInfoScreen!=null)then
			m_pxCampaignInfoScreen^.OnStartLevel();
		endif;
		return(true);
	endproc;
	*/
	
	export proc bool StartLoadGame()
		if(m_pxLoadSaveGameMenu!=null)then
			m_pxLoadSaveGameMenu^.OnLoadEnd();
		endif;
		return(true);
	endproc;
	
	export proc bool HostLobbyGame()
		if(m_pxNewMultiPlayerLobbyWindow!=null)then
			m_pxNewMultiPlayerLobbyWindow^.OnHost();
		endif;
		return(true);
	endproc;
	
	export proc bool HostSkirmish()
		if(m_pxMainMenu!=null)then
			m_pxMainMenu^.OnSkirmish();
		endif;
		return(true);
	endproc;
	
	export proc bool HostIPGame()
		if(m_pxMultiPlayerHostIPScreen!=null)then
			m_pxMultiPlayerHostIPScreen^.OnHost();
		endif;
		return(true);
	endproc;
	
	proc bool AddMenu(^CStateUIWnd p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.SetVisible(true);
		m_pxDesktop^.AddChild(p_pxWnd);
		if((p_pxWnd^.GetPos().GetX()==0)&&(p_pxWnd^.GetPos().GetY()==0))then
			p_pxWnd^.SetPos(m_iMenuOffsetX,m_iMenuOffsetY);
		endif;
		CWindowMgr.Get().SetModal(p_pxWnd);
		//CWindowMgr.Get().BringWindowToTop(p_pxWnd,true);
		return(true);
	endproc;
	
	proc bool AddMenu(^CHelpMenu p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.SetVisible(true);
		m_pxDesktop^.AddChild(p_pxWnd);
		if((p_pxWnd^.GetPos().GetX()==0)&&(p_pxWnd^.GetPos().GetY()==0))then
			p_pxWnd^.SetPos(m_iMenuOffsetX,m_iMenuOffsetY);
		endif;
		CWindowMgr.Get().SetModal(p_pxWnd);
		return(true);
	endproc;
	
	proc bool AddMenu(^CStaticCtrl p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.SetVisible(true);
		m_pxDesktop^.AddChild(p_pxWnd);
		if((p_pxWnd^.GetPos().GetX()==0)&&(p_pxWnd^.GetPos().GetY()==0))then
			p_pxWnd^.SetPos(m_iMenuOffsetX,m_iMenuOffsetY);
		endif;
		CWindowMgr.Get().SetModal(p_pxWnd);
		CWindowMgr.Get().BringWindowToTop(p_pxWnd,true);
		return(true);
	endproc;
	
	proc bool RemoveMenu(^CStateUIWnd p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.Destroy();
		p_pxWnd=null;
		return(true);
	endproc;
	
	proc bool RemoveMenu(^CHelpMenu p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.Destroy();
		p_pxWnd=null;
		return(true);
	endproc;
	
	proc bool RemoveMenu(^CStaticCtrl p_pxWnd)
		if(p_pxWnd==null)then return(false);endif;
		p_pxWnd^.Destroy();
		p_pxWnd=null;
		return(true);
	endproc;
	
	export proc ^CInGameScreen GetInGameScreen()
		return(m_pxInGameScreen);
	endproc;
	
	export proc ^CNewMultiPlayerPreGameWindow GetPreGameWindow()
		return m_pxNewMultiPlayerPreGameWindow;
	endproc;
	
	export proc bool GetUIShown()
		if(m_pxInGameScreen!=null)then return m_pxInGameScreen^.GetVisible();endif;
		return false;
	endproc;
	
	export proc bool ShowUI(bool p_bParam)
		if(p_bParam)then
			if(m_pxInGameScreen!=null)then m_pxInGameScreen^.SetVisible(true);endif;
		else
			if(m_pxInGameScreen!=null)then
				m_pxInGameScreen^.SetVisible(false);
			endif;
		endif;
		return(true);
	endproc;
	
	const int DONTCHANGEBACKGROUND = 0;
	const int RANDOMBACKGROUND = 1;
	const int CAMPAIGNBACKGROUND = 2;
	
	proc bool AddBackground()
		return AddBackground(DONTCHANGEBACKGROUND);
	endproc;
	
	proc bool AddBackground(int p_iState)
		CSoundMgrCln.SetGameIsRunning(false);
		var ^CDesktop pxDesktop=CClientWrap.GetDesktop();
		
		var int iShowBackgroundScene=1;
		if(!CSettings.Get("Game/GUIOptions/ShowBackgroundScene",iShowBackgroundScene)) then
			CSettings.Set("Game/GUIOptions/ShowBackgroundScene",iShowBackgroundScene);
		endif;

		if(iShowBackgroundScene==0) then return false; endif;
		if(pxDesktop^.BackgroundStreamIsEnabled() && p_iState==DONTCHANGEBACKGROUND) then return (true); endif;
		var string sBGVideo="";
//		if(p_iState==CAMPAIGNBACKGROUND)then
//			var string sLevel=CCampaignMgr.Get().GetLastEnabledCampaignLevel();
//			if(sLevel=="single_00" || sLevel=="tutorial") then sBGVideo="video/3d_menue/northland.bik"; endif;
//			if(sLevel=="single_01") then sBGVideo="video/3d_menue/northland.bik"; endif;
//			if(sLevel=="single_02") then sBGVideo="video/3d_menue/northland.bik"; endif;
//			if(sLevel=="single_03") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_04") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_05") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_06") then sBGVideo="video/3d_menue/jungle.bik"; endif;
//			if(sLevel=="single_07") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_08") then sBGVideo="video/3d_menue/icewaste.bik"; endif;
//			if(sLevel=="single_09") then sBGVideo="video/3d_menue/jungle.bik"; endif;
//			if(sLevel=="single_10") then sBGVideo="video/3d_menue/jungle.bik"; endif;
//			if(sLevel=="single_11") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_12") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_13") then sBGVideo="video/3d_menue/savanna.bik"; endif;
//			if(sLevel=="single_14") then sBGVideo="video/3d_menue/ashvalley.bik"; endif;
//			if(sLevel=="single_15") then sBGVideo="video/3d_menue/ashvalley.bik"; endif;
//			if(sLevel=="single_16") then sBGVideo="video/3d_menue/ashvalley.bik"; endif;
//		endif;
//		if(sBGVideo=="")then
//			// We need always random because AyCe modded the campaign too much xD
//			var CConfig xConf;
//			var int iSel=xConf.GetSetI("Client/GameplayOptions/DefBGVideo",-1);
//			var array string asBGVideos;
//			asBGVideos.AddEntry("video/3d_menue/northland.bik");
//			asBGVideos.AddEntry("video/3d_menue/savanna.bik");
//			asBGVideos.AddEntry("video/3d_menue/jungle.bik");
//			asBGVideos.AddEntry("video/3d_menue/icewaste.bik");
//			asBGVideos.AddEntry("video/3d_menue/ashvalley.bik");
//			asBGVideos.AddEntry("video/3d_menue/oasis.bik");
//			var int iRandomIndex = Random.GetInt()%asBGVideos.NumEntries();
//			if(iSel<0||iSel>4)then
//				iSel = Random.GetInt()%asBGVideos.NumEntries();
//			endif;
//			sBGVideo=asBGVideos[iRandomIndex];
//			sBGVideo=asBGVideos[iSel];
//		endif;

		var int iSel = Random.GetInt() % 5 ;
		var string sOverlay = "menu_overlay_";
		var string sBGImage;
		var string sSetting;
		if(iSel==0)then
			sSetting = "nor";
			sBGVideo = "video/3d_menue/northland_mirage.bik";
		elseif(iSel==1)then
			sSetting = "sav";
			sBGVideo = "video/3d_menue/savanna.bik";
		elseif(iSel==2)then
			sSetting = "jng";
			sBGVideo = "video/3d_menue/jungle_mirage.bik";
		elseif(iSel==3)then
			sSetting = "ice";
			sBGVideo = "video/3d_menue/icewaste.bik";
		elseif(iSel==4)then
			sSetting = "ash";
			sBGVideo = "video/3d_menue/ashvalley_mirage.bik";
		elseif(iSel==5)then
			sSetting="oas";
			sBGVideo = "video/3d_menue/oasis.bik";
		else
			sSetting = "nor";
			sBGVideo = "video/3d_menue/northland.bik";
		endif;
		//sBGVideo = "video/3d_menue/bg_video_" + sSetting + ".bik";
		sBGImage = "menue/decoration/menu_overlay_" + sSetting + ".tga";

		if(pxDesktop^.BackgroundStreamIsEnabled() && m_sCurBGVideo==sBGVideo)then return true; endif;
		
		if(m_pxMenuOverlay!=null) then
			m_pxMenuOverlay^.Destroy();
			m_pxMenuOverlay=null;
		endif;
		m_pxMenuOverlay=new CMenuOverlay(true);
		
		m_pxTmp=new CStaticCtrl();
		m_pxTmp^.SetBitmap(sBGImage);	
		m_pxTmp^.SetTransparent(true);
		m_pxTmp^.SetBackground(true);
		pxDesktop^.AddChild(m_pxTmp);
		
		m_sCurBGVideo=sBGVideo;
		pxDesktop^.BackgroundStreamEnable(true);
		pxDesktop^.BackgroundStreamSetFadeTime(1.0);
		pxDesktop^.BackgroundStreamSwitch(sBGVideo,false);
		return(true);
	endproc;
	
	proc void RemBackground()
		var ^CDesktop 	pxDesktop=CClientWrap.GetDesktop();
		pxDesktop^.BackgroundStreamEnable(false);
		if(m_pxTmp!=null) then
			//m_pxTmp^.ClearBitmap();
			pxDesktop^.RemoveChild(m_pxTmp);
		endif;
	endproc;
	
	export proc void SetISVisible(bool p_bVisible)
		if(m_pxInGameScreen!=null)then
			m_pxInGameScreen^.SetVisible(p_bVisible);
		endif;
	endproc;
	
	export proc bool OnShowToolTip(string p_sTitle, string p_sString)
		if((m_pxInGameScreen!=null)&&(m_pxInGameScreen^.GetVisible()))then
			m_pxToolTip^.SetText("  "+p_sTitle+p_sString);
			if(m_pxMenuOverlay!=null)then
				m_pxMenuOverlay^.SetToolTip(p_sString);
			endif;
		else
			if(p_sString.GetLength()==0)then
//				p_sString=CVersionInfo.GetPublicVersion();
			endif;
			ShowMenuToolTip(p_sTitle+p_sString);
		endif;
		return(false);
	endproc;

	export proc bool OnTick()
		if(GetInGameScreen()!=null)then
			GetInGameScreen()^.OnTick();
		endif;
		return(true);
	endproc;
	
	export proc bool SetIPAddress(string p_sAddress)
		CUIStateMgr.CGameInfo.m_sIPAddress=p_sAddress;
		return(true);
	endproc;
	
	export proc bool SetNewProfileName(string p_sName)
		if(m_pxOptionsMenu!=null)then
			m_pxOptionsMenu^.GetProfilesOptions()^.SetNewProfileName(p_sName);
		endif;
		return(true);
	endproc;
	
	// calls the correct StateChanges if the server/connction was lost
	// this procref should be used only with the method HandleServerLost()
	export proc void ChangeTheStateAfterServerLost(int p_iUnused)
		//L KLog.LogSpam("MaSc", "CUIStateMgr::HandleServerLost");
		if(m_iState==CUIStateMgr.STATE_NEWMULTIPLAYERPREGAME)then
			//L KLog.LogSpam("MaSc","Server Lost in NewMultiPlayerpregame Window back to Newmultiplayerlobby");
			CUIStateMgr.Get().SetState(CUIStateMgr.STATE_ACKSERVERLOST);
		elseif(m_iState==CUIStateMgr.STATE_INGAME)then
			//L KLog.LogSpam("MaSc","Server Lost In Game back to MainMenü");
			CUIStateMgr.Get().SetState(CUIStateMgr.STATE_ACKSERVERLOST);
		else
			//L KLog.LogSpam("MaSc","Server lost in UIState "+m_iState.ToString());
		endif;
	endproc;
	
	export proc bool HandleServerLost()
		// handle server lost only for clients
		if(!CGameWrap.IsClientWithHost())then	
			ChangeTheStateAfterServerLost(-1); 		
		endif;
		return true;
	endproc;	
	
	export proc void OnQuitMsgBox_CallBack(int p_iResult)
		var bitset dwResult = Math.IntToBitset(p_iResult);
		if(dwResult==CMessageBox.SID_YES)then
			CGameWrap.Quit();
		else
			if(m_pxGameMenu!=null)then
				m_pxGameMenu^.SetVisible(true);
			endif;
		endif;
	endproc;
	
endclass;


class CStateUIWnd inherit CFrameWindow
	
	var int						m_iReturnState;
	
	export constructor()
		SetStyle(CFrameWindow.WS_FixedPos | CFrameWindow.WS_Caption);
		m_iReturnState=CUIStateMgr.Get().GetState();
	endconstructor;

	destructor()
	enddestructor;
	
	export proc int GetReturnState()
		return(m_iReturnState);
	endproc;
	
	export proc void SetReturnState(int p_iReturnState)
		m_iReturnState=p_iReturnState;
	endproc;
	
	proc bool SetWindowTitle(string p_sTitle)
		//Test
		SetCaption(p_sTitle);
		//SetStyle(CFrameWindow.WS_FixedPos | CFrameWindow.WS_Caption);
		//m_pxTitle^.SetText(p_sTitle);
		return(true);
	endproc;
	
	proc void SetCaption(string p_sTitle)
		super.SetCaption(p_sTitle);
		//SetStyle(CFrameWindow.WS_FixedPos | CFrameWindow.WS_Caption);
	endproc;
	
endclass;
